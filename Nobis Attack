!scriptcard{{
	--#title|Nobis Attack Macro
	--#sourceToken|@{selected|token_id}
	--#targetToken|@{target|token_id}

	--=TargetAC|[*T:ac]
	--? [*T:npc_ac] -lt [$TargetAC]|SkipAC
	--=TargetAC|[*T:npc_ac]
	--:SkipAC|

  	--~Distance|distance;[*S:t-id];[*T:t-id]
  	--=Distance|[$Distance] * 5

	--+COVER|[rbutton]Half Cover::HALF_COVER[/rbutton]
	--+COVER|[rbutton]3/4 Cover::3Q_COVER[/rbutton]
	--:COVER_RETURN|

	--+WEAPON|[rbutton]Dagger +1::DAGGER_PLUS_1[/rbutton]
	--+WEAPON|[rbutton]Switch Sword::SWITCH_SWORD[/rbutton]
	--+WEAPON|[rbutton]Switch Dagger::SWITCH_DAGGER[/rbutton]
	--+WEAPON|Revolver 1 [rbutton]Fire::REVOLVER;1[/rbutton] @{selected|repeating_resource_$1_resource_left} / 6 bullets [rbutton]Reload::RELOAD_REVOLVER;1[/rbutton]
	--+WEAPON|Revolver 2 [rbutton]Fire::REVOLVER;2[/rbutton] @{selected|repeating_resource_$2_resource_left} / 6 bullets [rbutton]Reload::RELOAD_REVOLVER;2[/rbutton]
	--+WEAPON|[rbutton]Scimitar::SCIMITAR[/rbutton] [rbutton]Momentum::SCIMITAR;m[/rbutton]
	--? [*S:other_resource] -lt 1|SKIP_HAND_CROSSBOW
	--+WEAPON|[rbutton]Hand Crossbow::HAND_CROSSBOW[/rbutton] [*S:other_resource] bolts
	--:SKIP_HAND_CROSSBOW|
	--? [*S:repeating_resource_$4_resource_left] -lt 1|SKIP_THROWING_DAGGER
	--+WEAPON|[rbutton]Throwing Dagger::THROWING_DAGGER[/rbutton] @{selected|repeating_resource_$4_resource_left} daggers
	--:SKIP_THROWING_DAGGER|

	--:INIT_VALUES|
	--=Damage1Count|0
	--=Damage1Die|0
	--&Damage1Type|NULL
	--=Damage2Count|0
	--=Damage2Die|0
	--&Damage2Type|NULL
	--&SEffectString|[*S:t-bar2_value]
	--&TEffectString|[*T:t-bar2_value]
	--=BardicFlag|0
	--=BardicDie|@{Nobis|BardicInspirationDie}
	--=CritFlag|0
	--&AttackType|NULL
	--&WeaponName|NULL
	--=DamageFlag|0
	--&WeaponTags|NULL
	--&DeflectString|NULL

	--? [&TEffectString] -inc Sunder|>APPLY_SUNDER
	--X|

	--:HALF_COVER|
		--+COVER|Half Cover: AC + 2!
		--=TargetAC|[$TargetAC] + 2
	--^COVER_RETURN|

	--:3Q_COVER|
		--+COVER|3/4 Cover: AC + 5!
		--=TargetAC|[$TargetAC] + 5
	--^COVER_RETURN|

	--:APPLY_SUNDER|
		--~AfterStart|string;after;Sunder:;[&TEffectString]
		--~Num|string;before;:;[&AfterStart]

		--=Num|[&Num]

		--+SUNDER|Target was sundered, reduced AC!
		--=TargetAC|[$TargetAC] - [$Num]
	--<|

	--:DAGGER_PLUS_1|
		--=Damage1Count|1
		--=Damage1Die|4
		--&Damage1Type|Magical Piercing
		--&DamageMod|[*S:dexterity_mod] + 1
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb] + 1
		--&AttackType|DEX
		--&WeaponName|Dagger +1
		--&WeaponTags|Balanced
		-->DEFLECT_STRING|
		--=NormalRange|30
		--=LongRange|90
		-->CHECK_DISTANCE|
	--^SELECT_ATTACK|

	--:SWITCH_SWORD|
		--=Damage1Count|1
		--=Damage1Die|6
		--&Damage1Type|Magical Slashing
		--&DamageMod|[*S:dexterity_mod]
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb]
		--&AttackType|DEX
		--&WeaponName|Switch Sword
		--&WeaponTags|Balanced
		-->DEFLECT_STRING|
	--^SELECT_ATTACK|

	--:SWITCH_DAGGER|
		--=Damage1Count|1
		--=Damage1Die|4
		--&Damage1Type|Magical Piercing
		--&DamageMod|[*S:dexterity_mod]
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb]
		--&AttackType|DEX
		--&WeaponName|Switch Sword
		--&WeaponTags|Balanced
		-->DEFLECT_STRING|
		--=NormalRange|30
		--=LongRange|90
		-->CHECK_DISTANCE|
	--^SELECT_ATTACK|

	--:REVOLVER|
		--&Revolver|[&reentryval]
		--? [*S:repeating_resource_$[&Revolver]_resource_left] -lt 1|NOT_ENOUGH_AMMO
		--@modattr| _charid [*S:id] _repeating_resource_$[&Revolver]_resource_left|-1 _silent
		--=Damage1Count|2
		--=Damage1Die|6
		--&Damage1Type|Piercing
		--&DamageMod|[*S:dexterity_mod]
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb]
		--&AttackType|DEX
		--&WeaponName|Revolver [&Revolver]
		--&WeaponTags|Puncture
		-->DEFLECT_STRING|
		--=NormalRange|80
		--=LongRange|320
		-->CHECK_DISTANCE|
	--^SELECT_ATTACK|

	--:RELOAD_REVOLVER|
		--&Revolver|[&reentryval]
		--=CurrentAmmo|[*S:repeating_resource_$[&Revolver]_resource_left]
		--=ReserveAmmo|[*S:repeating_resource_$1_resource_right]
		--=MaxAmmo|6
		--=AmmoToLoad|[$MaxAmmo] - [$CurrentAmmo]
		--? [$AmmoToLoad] -gt [$ReserveAmmo]|PARTIAL_RELOAD_REVOLVER
		--=RemainingReserve|[$ReserveAmmo] - [$AmmoToLoad]
		--+AMMO|[$MaxAmmo] / [$MaxAmmo] bullets
		--+RESERVE|[$RemainingReserve] bullets
		--@setattr| _charid [*S:id] _repeating_resource_$[&Revolver]_resource_left|[$MaxAmmo] _silent
		--@setattr| _charid [*S:id] _repeating_resource_$1_resource_right|[$RemainingReserve] _silent
	--X|

	--:PARTIAL_RELOAD_REVOLVER|
		--=CurrentAmmo|[$CurrentAmmo] + [$ReserveAmmo]
		--+AMMO|[$CurrentAmmo] / [$MaxAmmo] bullets
		--+RESERVE|Reserve Depleted!
		--@setattr| _charid [*S:id] _repeating_resource_$[&Revolver]_resource_left|[$CurrentAmmo] _silent
		--@setattr| _charid [*S:id] _repeating_resource_$1_resource_right|0 _silent
	--X|

	--:SCIMITAR|
		--=Damage1Count|1
		--? [&reentryval] -eq m|>MOMENTUM_BONUS_DIE
		--=Damage1Die|8
		--&Damage1Type|Magical Slashing
		--&DamageMod|[*S:dexterity_mod]
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb]
		--&AttackType|DEX
		--&WeaponType|MELEE
		--&WeaponName|Scimitar
		--&DeflectString|Deflect:[$Damage1Count.Raw]d[$Damage1Die.Raw]+[*S:dexterity_mod]
		--&WeaponTags|Keen
		-->DEFLECT_STRING|
	--^SELECT_ATTACK|

	--:MOMENTUM_BONUS_DIE|
		--=Damage1Count|2
	--<|

	--:HAND_CROSSBOW|
		--@modattr| _charid [*S:id] _other_resource|-1 _silent
		--=Damage1Count|1
		--=Damage1Die|6
		--&Damage1Type|Piercing
		--&DamageMod|[*S:dexterity_mod]
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb]
		--&AttackType|DEX
		--&WeaponType|RANGED
		--&WeaponName|Hand Crossbow
		--&WeaponTags|Keen
		-->DEFLECT_STRING|
		--=NormalRange|30
		--=LongRange|120
		-->CHECK_DISTANCE|
	--^SELECT_ATTACK|

	--:THROWING_DAGGER|
		--@modattr| _charid [*S:id] _repeating_resource_$4_resource_left|-1 _silent
		--=Damage1Count|1
		--=Damage1Die|6
		--&Damage1Type|Piercing
		--&DamageMod|[*S:dexterity_mod]
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb]
		--&AttackType|DEX
		--&WeaponName|Throwing Dagger
		-->DEFLECT_STRING|
		--=NormalRange|30
		--=LongRange|90
		-->CHECK_DISTANCE|
	--^SELECT_ATTACK|

	--:DEFLECT_STRING|
		--? "[&SEffectString]" -inc "Deflect"|>REMOVE_DEFLECT
		--? "[&DeflectString]" -eq "NULL"|SKIP_DEFLECT_STRING
		--&SEffectString|+[&DeflectString]
		--:SKIP_DEFLECT_STRING|
		--@token-mod| _ids [*S:t-id] _set bar2_value|[&SEffectString]
	--<|

	--:REMOVE_DEFLECT|
		--~BeforeDeflect|string;before;Deflect:;[&SEffectString]
		--~AfterDeflect|string;after;Deflect:;[&SEffectString]
		--~AfterMarker|string;after;:;[&AfterDeflect]
		--&SEffectString|[&BeforeDeflect][&AfterMarker]
	--<|

	--:CHECK_DISTANCE|
		--? [$Distance] -le [$NormalRange]|SKIP_DISTANCE_WARNING
		--? [$Distance] -gt [$LongRange]|ERROR_RANGE
		--+WARNING|You are attacking from further ([$Distance] ft) than the Normal Range ([$NormalRange] ft) but closer than Max Range ([$LongRange] ft). Unless you have a Feature stating otherwise, factor Disadvantage into your Attack.
		--:SKIP_DISTANCE_WARNING|
	--<|

	--:ERROR_RANGE|
		--+FAILURE|Current Attack Distance ([$Distance] ft) further than Max Range ([$LongRange] ft). Attack Canceled.
	--X|

	--:SELECT_ATTACK|
		--? [&SEffectString] -inc Invisible|>INVIS_EMOTE
		--? [&SEffectString] -ninc Invisible|>NORMAL_EMOTE
		--+ATTACK|[rbutton]Advantage::TO_HIT;2d20kh1[/rbutton]
		--+ATTACK|[rbutton]Normal::TO_HIT;1d20[/rbutton]
		--+ATTACK|[rbutton]Disadvantage::TO_HIT;2d20kl1[/rbutton]
	--X|

	--:INVIS_EMOTE|
		--#emoteText|Something attacks @{target|token_name} with ...something?
	--<|

	--:NORMAL_EMOTE|
		--#emoteText|@{selected|token_name} attacks @{target|token_name} with their [&WeaponName]
	--<|

	--:TO_HIT|
		--&SEffectString|[*S:t-bar2_value]
		--&TEffectString|[*T:t-bar2_value]	
		--&RollString|[&reentryval] + [&ToHitMod]

		--? [&SEffectString] -inc Bless|>BLESS
		--? [&SEffectString] -inc Bane|>BANE
		--? [&SEffectString] -inc BolsteringMagic|>BOLSTERING_MAGIC

		--+ROLL|[&RollString]
		--=ToHit|[&RollString]
		--+TO HIT|[$ToHit]

		--? [&TEffectString] -inc MirrorImage|>MIRROR_IMAGE

		--? [$ToHit.Base] -eq 20|CRITICAL
		--? [$ToHit.Base] -eq 1|FUMBLE
		--:BARDIC_RETURN|
		--? [$ToHit] -ge [$TargetAC]|HIT
		--? [$ToHit] -lt [$TargetAC]|MISS
	--X|

	--:BLESS|
 		--=Bless|1d4
 		--+Bless|[$Bless]
 		--&RollString|+ + [$Bless.Raw]
	--<|

	--:BANE|
 		--=Bane|1d4
 		--+Bane|[$Bane]
 		--&RollString|+ - [$Bane.Raw]
	--<|

	--:BOLSTERING_MAGIC|
 		--=Bolster|1d3
 		--+Bolstering Magic|[$Bolster]
 		--&RollString|+ + [$Bolster.Raw]
	--<|

	--:MIRROR_IMAGE|
		--~AfterMirror|string;after;MirrorImage:;[&TEffectString]
		--~ImageCount|string;before;:;[&AfterMirror]

		--=ImageCount|[&ImageCount]

		--=MirrorImageRoll|1d20
		--+TEST|[$MirrorImageRoll]
		--=MirrorImageAC|10 + [*T:dexterity_mod]

		--? [$ImageCount] -eq 1|1_IMAGE
		--? [$ImageCount] -eq 2|2_IMAGE
		--? [$ImageCount] -eq 3|3_IMAGE
		--:MIRROR_IMAGE_RETURN|

		--? [$ToHit] -ge [$MirrorImageAC]|MIRROR_IMAGE_HIT
		--? [$ToHit] -lt [$MirrorImageAC]|MIRROR_IMAGE_MISS

		--:MIRROR_IMAGE_CANCEL|
	--<|

	--:1_IMAGE|
		--? [$MirrorImageRoll] -lt 11|MIRROR_IMAGE_CANCEL
	--^MIRROR_IMAGE_RETURN|

	--:2_IMAGE|
		--? [$MirrorImageRoll] -lt 8|MIRROR_IMAGE_CANCEL
	--^MIRROR_IMAGE_RETURN|

	--:3_IMAGE|
		--? [$MirrorImageRoll] -lt 6|MIRROR_IMAGE_CANCEL
	--^MIRROR_IMAGE_RETURN|

	--:MIRROR_IMAGE_HIT|
		--+HIT|Your Attack was Directed at a Mirror Image and Hit the Image!

		--~Before|string;before;:MirrorImage;[&TEffectString]
		--~After|string;after;:;[&AfterMirror]

		--=ImageCount|[$ImageCount] - 1

		--? [$ImageCount] -eq 0|>REMOVE_MIRROR_IMAGE
		--? [$ImageCount] -gt 0|>DECREMENT_MIRROR_IMAGE

  		--@token-mod| _ids [*T:t-id] _ignore-selected _set bar2_value|[&TEffectString]
	--X|

	--:REMOVE_MIRROR_IMAGE|
		--&TEffectString|[&Before][&After]
	--<|

	--:DECREMENT_MIRROR_IMAGE|
		--&TEffectString|[&Before][&After]:MirrorImage:[$ImageCount.Raw]:
	--<|

	--:MIRROR_IMAGE_MISS|
		--+MISS|Your Attack was Directed at a Mirror Image and Missed the Image!
	--X|

	--:CRITICAL|
		--? [&TEffectString] -inc CritNegate|NEGATE_CRIT
		--=CritBonus|[$Damage1Count.Raw] * [$Damage1Die.Raw]
		--&DamageMod|+ + [$CritBonus] [CRIT]
		--=Damage2Count|[$Damage2Count] * 2
		--? [&WeaponTags] -inc Keen|>KEEN_CRIT
		--+CRITICAL|[rbutton]Critical Hit!::DAMAGE[/rbutton]
		--? [*S:class_resource] -lt 1|SKIP_CRIT_FLOURISH
		--+FLOURISH|[rbutton]Defensive Flourish::DEFENSIVE_FLOURISH[/rbutton]
		--+FLOURISH|[rbutton]Slashing Flourish::SLASHING_FLOURISH[/rbutton]
		--+FLOURISH|[rbutton]Mobile Flourish::MOBILE_FLOURISH[/rbutton]
		--:SKIP_CRIT_FLOURISH|
		--=CritFlag|1
	--X|

	--:KEEN_CRIT|
		--=Damage1Count|[$Damage1Count] + 1
	--<|

	--:NEGATE_CRIT|
		--+CRITICAL|Critical Hit!
		--+NEGATED|Critical Negated!
		--+HIT|[rbutton]Hit!::DAMAGE[/rbutton]
		--? [*S:class_resource] -lt 1|SKIP_NEG_CRIT_FLOURISH
		--+FLOURISH|[rbutton]Defensive Flourish::DEFENSIVE_FLOURISH[/rbutton]
		--+FLOURISH|[rbutton]Slashing Flourish::SLASHING_FLOURISH[/rbutton]
		--+FLOURISH|[rbutton]Mobile Flourish::MOBILE_FLOURISH[/rbutton]
		--:SKIP_NEG_CRIT_FLOURISH|
	--X|

	--:FUMBLE|
		--+FUMBLE|Critical Failure.
	--X|

	--:HIT|
		--+HIT|[rbutton]Hit!::DAMAGE[/rbutton]
		--? [*S:class_resource] -lt 1|SKIP_HIT_FLOURISH
		--+FLOURISH|[rbutton]Defensive Flourish::DEFENSIVE_FLOURISH[/rbutton]
		--+FLOURISH|[rbutton]Slashing Flourish::SLASHING_FLOURISH[/rbutton]
		--+FLOURISH|[rbutton]Mobile Flourish::MOBILE_FLOURISH[/rbutton]
		--:SKIP_HIT_FLOURISH|
	--X|

	--:MISS|
		--+MISS|Miss.
		--? [&SEffectString] -inc BardicInspiration|>PROMPT_BARDIC
	--X| 

	--:PROMPT_BARDIC|
		--? [$BardicFlag] -eq 1|SKIP_BARDIC
		--+INSPIRED|[rbutton]Use Bardic Inspiration?::BARDIC_INSPIRATION[/rbutton]
		--:SKIP_BARDIC|
	--<|

	--:BARDIC_INSPIRATION|
		--? [$BardicFlag] -eq 1|ERROR_BARDIC
		--=BardicFlag|1
		--=Bardic|1d[$BardicDie.Raw]
		--+Bardic Roll|[$Bardic]
		--=ToHit|[$ToHit] + [$Bardic]
		--+New To Hit|[$ToHit]

		--~Before|string;before;BardicInspiration;[&SEffectString]
		--~After|string;after;BardicInspiration;[&SEffectString]

		--&SEffectString|[&Before][&After]
		--@token-mod| _ids [*S:t-id] _set bar2_value|[&SEffectString]
	--^BARDIC_RETURN|

	--:DEFENSIVE_FLOURISH|
		--@modattr| _charid [*S:id] _class_resource|-1 _silent
		--=FlourishRoll|1d[$BardicDie]
		--+FLOURISH|Defensive Flourish
		--+FLOURISH|Bonus AC: [$FlourishRoll]
		--=FlourishDamage|[$FlourishRoll]
		--? [$CritFlag] -eq 1|>FLOURISH_CRIT
		--+FLOURISH|[$FlourishDamage] [&Damage1Type] DAMAGE
		--&DamageMod|+ + [$FlourishDamage]
	--^DAMAGE|

	--:SLASHING_FLOURISH|
		--@modattr| _charid [*S:id] _class_resource|-1 _silent
		--=FlourishRoll|1d[$BardicDie]
		--+FLOURISH|Defensive Flourish
		--+FLOURISH|Extra Slashing Damage: [$FlourishRoll] [&Damage1TypeDamage] DAMAGE
		--=FlourishDamage|[$FlourishRoll]
		--? [$CritFlag] -eq 1|>FLOURISH_CRIT
		--+FLOURISH|[$FlourishDamage] [&Damage1Type] DAMAGE
		--&DamageMod|+ + [$FlourishDamage]
	--^DAMAGE|

	--:MOBILE_FLOURISH|
		--@modattr| _charid [*S:id] _class_resource|-1 _silent
		--=FlourishRoll|1d[$BardicDie]
		--+FLOURISH|Defensive Flourish
		--=Movement|5 + [$FlourishRoll]
		--+FLOURISH|Target Moved: [$Movement]
		--=FlourishDamage|[$FlourishRoll]
		--? [$CritFlag] -eq 1|>FLOURISH_CRIT
		--+FLOURISH|[$FlourishDamage] [&Damage1Type] DAMAGE
		--&DamageMod|+ + [$FlourishDamage]
	--^DAMAGE|

	--:FLOURISH_CRIT|
		--=FlourishDamage|[$FlourishRoll] + 1d[$BardicDie]
	--<|

	--:DAMAGE|		
		--&SEffectString|[*S:t-bar2_value]
		--&TEffectString|[*T:t-bar2_value]	
		--? [$DamageFlag] -eq 1|ERROR_DAMAGE
		--=DamageFlag|1
		--=Damage1|0
		--=Damage2|0

		--&Damage1String|[$Damage1Count.Raw]d[$Damage1Die.Raw]
		--? [$Damage2Count] -eq 0|SkipDamage2a
		--&Damage2String|[$Damage2Count.Raw]d[$Damage2Die.Raw]
		--:SkipDamage2a|

		--? "[&WeaponTags]" -inc "Puncture"|>ADD_PUNCTURE
		--? "[&WeaponTags]" -inc "Balanced"|>ADD_BALANCED

		--? [&SEffectString] -inc Enlarged|>ENLARGED
		--? [&SEffectString] -inc Reduced|>REDUCED

		--&Damage1String|+ + [&DamageMod]

		--=Damage1|[&Damage1String]
		--+[&Damage1Type] Damage|[$Damage1]
		--? [$Damage2Count] -eq 0|SkipDamage2b
		--=Damage2|[&Damage2String]
		--+[&Damage2Type] Damage|[$Damage2]
		--:SkipDamage2b|

		--? [&SEffectString] -inc Enfeebled|>ENFEEBLED

		--? "[*T:npc_vulnerabilities]" -inc "[&Damage1Type]"|>VULN_1
		--? "[*T:npc_vulnerabilities]" -inc "[&Damage2Type]"|>VULN_2
		--? "[*T:npc_resistances]" -inc "[&Damage1Type]"|>RESIST_1
		--? "[*T:npc_resistances]" -inc "[&Damage2Type]"|>RESIST_2
		--? "[*T:npc_immunities]" -inc "[&Damage1Type]"|>IMMUNE_1
		--? "[*T:npc_immunities]" -inc "[&Damage2Type]"|>IMMUNE_2

		--? [&TEffectString] -inc Underwater|>UNDERWATER

		--=TotalDamage|[$Damage1] + [$Damage2]

		--? "[&TEffectString]" -inc "DT"|>APPLY_DAMAGE_THRESHOLD

		--+TotalDamage|[$TotalDamage]

		--? [&TEffectString] -inc Retaliation|>RETALIATION
	--^APPLY_DAMAGE|

	--:ADD_PUNCTURE|
		--&Damage1String|+!
	--<|

	--:ADD_BALANCED|
		--&Damage1String|[&Damage1String]ro<1
	--<|

	--:ENLARGED|
		--? [&AttackType] -ne STR|ENLARGED_SKIP
		--? [$CritFlag] -eq 1|ENLARGED_CRIT
		--&Damage1String|+ + 1d4
		--:ENLARGED_CRIT_RETURN|
		--:ENLARGED_SKIP|
	--<|

	--:ENLARGED_CRIT|
		--&Damage1String|+ + 2d4
	--^ENLARGED_CRIT_RETURN|

	--:REDUCED|
		--&Damage1String|+ - 1d4
	--<|
	
	--:ENFEEBLED|
		--? [&AttackType] -ne STR|SKIP_ENFEEBLED
		--+ENFEEBLED|Damage Halved!
		--=Damage1|[$Damage1] \ 2
		--:SKIP_ENFEEBLED|
	--<|

	--:VULN_1|
		--=Damage1|[$Damage1] * 2
	--<|

	--:VULN_2|
		--=Damage2|[$Damage2] * 2
	--<|

	--:RESIST_1|
		--=Damage1|[$Damage1] \ 2
	--<|

	--:RESIST_2|
		--=Damage2|[$Damage2] \ 2
	--<|

	--:IMMUNE_1|
		--=Damage1|0
	--<|

	--:IMMUNE_2|
		--=Damage2|0
	--<|

	--:UNDERWATER|
		--? "[*T:npc_resistances]" -inc "Fire"|SKIP_UNDERWATER
		--? [&Damage1Type] -eq Fire|>RESIST_1
		--? [&Damage2Type] -eq Fire|>RESIST_2
		--:SKIP_UNDERWATER|
	--<|

	--:APPLY_DAMAGE_THRESHOLD|
		--+DT|Applying Damage Threshold...

		--~AfterString|string;after;DT;[&EffectString2]
		--~DT|string;left;2;[&AfterString]
		--=DT|[&DT]

		--? [$TotalDamage] -le [$DT]|[
			--=TotalDamage|0
			--+Resisted|@{target|character_name} Resisted the Attack!
		--]|
	--<|

	--:APPLY_DAMAGE|
		--? @{target|bar3} -gt 0|APPLY_TEMP_DAMAGE
		--@alter|_target|@{target|token_id} _bar|1 _amount|-[$TotalDamage]
		--^MARK_TOKEN|

	--:APPLY_TEMP_DAMAGE|
		--? @{target|bar3} -lt [$TotalDamage]|APPLY_TEMP_PARTIAL
		--@alter|_target|@{target|token_id} _bar|3 _amount|-[$TotalDamage]
		--^MARK_TOKEN|

	--:APPLY_TEMP_PARTIAL|
		--=RemainderDamage|[$TotalDamage] - @{target|bar3}
		--@alter|_target|@{target|token_id} _bar|3 _amount|-@{target|bar3}
		--@alter|_target|@{target|token_id} _bar|1 _amount|-[$RemainderDamage]
		--^MARK_TOKEN|

	--:MARK_TOKEN|
		--=HalfHP|[*T:hp^] \ 2
		--=HPTotal|@{target|bar1} + @{target|bar3}
		--=EstFinalHP|[$HPTotal] - [$TotalDamage]
		--? [$EstFinalHP] -le [$HalfHP] -and [$EstFinalHP] -gt 0|MARK_TOKEN_RED
		--? [$EstFinalHP] -le 0|MARK_TOKEN_DEAD
	--X|

  	--:MARK_TOKEN_RED|
  		--@token-mod| _ids [*T:t-id] _ignore-selected _set statusmarkers#red
  	--X|

  	--:MARK_TOKEN_DEAD|
  		--@token-mod| _ids [*T:t-id] _ignore-selected _set statusmarkers#dead|-red
  	--X|

  	--:ERROR_BARDIC|
  		--+ERROR|You have already used Bardic Inspiration!
  	--X|

  	--:ERROR_DAMAGE|
  		--+ERROR|You have already applied Damage!
  	--X|

  	--:RETALIATION|
  		--=RetaliationFlag|1

  		--~AfterMark|string;after;Retaliation:;[&TEffectString]
  		--~AfterMark2|string;after;:;[&AfterMark]
  		--~RetaliationDamageString|string;before;:;[&AfterMark]
  		--~RetaliationDamageType|string;before;:;[&AfterMark2]

  		--=RetaliationDamage|[&RetaliationDamageString]
  		
  		--? [&TEffectString] -inc TempHealthRetaliation|>TEMP_HEALTH_RETALIATION
  		--? [&TEffectString] -inc MeleeRetaliation|>MELEE_RETALIATION
  		--? [&TEffectString] -inc DistanceRetaliation|>DISTANCE_RETALITION

  		--? [$RetaliationFlag] -eq 0|SKIP_RETALIATION

		--? "[*S:npc_vulnerabilities]" -inc "[&RetaliationDamageType]"|>VULN_RETLATIATION
		--? "[*S:npc_resistances]" -inc "[&RetaliationDamageType]"|>RESIST_RETLATIATION
		--? "[*S:npc_immunities]" -inc "[&RetaliationDamageType]"|>IMMUNE_RETLATIATION

		--+RETALIATION|[$RetaliationDamage] [&RetaliationDamageType] Damage

		--? [*S:t-bar3_value] -eq 0|>RETALIATION_APPLY_DAMAGE
		--? [*S:t-bar3_value] -le [$RetaliationDamage]|>RETALIATION_APPLY_PARTIAL_DAMAGE
		--? [*S:t-bar3_value] -gt [$RetaliationDamage]|>RETALIATION_APPLY_TEMP_DAMAGE

		--:SKIP_RETALIATION|
  	--<|

  	--:TEMP_HEALTH_RETALIATION|
  		--? [&WeaponType] -eq RANGED|>INVALID_RETALIATION
  		--? [$RetaliationDamage] -ge [*T:t-bar3_value]|>TEMP_HEALTH_RETALIATION_REMOVE
  	--<|

  	--:MELEE_RETALIATION|
  		--? [&WeaponType] -eq RANGED|>INVALID_RETALIATION
  	--<|

  	--:DISTANCE_RETALITION|
  		--? [&WeaponType] -eq RANGED|>INVALID_RETALIATION

  		--~AfterMark3|string;after;:;[&AfterMark2]
  		--~MaxDistance|string;before;:;[&AfterMark3]
  		--=MaxDistance|[&MaxDistance]

  		--? [$Distance] -gt [$MaxDistance]|>INVALID_RETALIATION
  	--<|

	--:VULN_RETLATIATION|
		--=RetaliationDamage|[$RetaliationDamage] * 2
	--<|

	--:RESIST_RETLATIATION|
		--=RetaliationDamage|[$RetaliationDamage] \ 2
	--<|

	--:IMMUNE_RETLATIATION|
		--=RetaliationDamage|0
	--<|

	--:INVALID_RETALIATION|
		--=RetaliationFlag|0
	--<|

	--:TEMP_HEALTH_RETALIATION_REMOVE|
		--+TEST|REMOVE
		--~BeforeRetaliation|string;before;TempHealthRetaliation;[&TEffectString]
		--~AfterRetaliation|string;after;:;[&AfterMark2]

		--&TEffectString|[&BeforeRetaliation][&AfterRetaliation]

		--@token-mod| _ids [*T:t-id] _set bar2_value|[&TEffectString]
	--<|

	--:RETALIATION_APPLY_DAMAGE|
		--@alter|_target|@{selected|token_id} _bar|1 _amount|-[$RetaliationDamage]
		--^MARK_SOURCE_TOKEN|
	--<|

	--:RETALIATION_APPLY_PARTIAL_DAMAGE|
		--=RemainderDamage|[$RetaliationDamage] - [*S:t-bar3_value]
		--@alter|_target|@{selected|token_id} _bar|3 _amount|-[*S:t-bar3_value]
		--@alter|_target|@{selected|token_id} _bar|1 _amount|-[$RemainderDamage]
		--^MARK_SOURCE_TOKEN|
	--<|

	--:RETALIATION_APPLY_TEMP_DAMAGE|
		--@alter|_target|@{selected|token_id} _bar|3 _amount|-[$RetaliationDamage]
		--^MARK_SOURCE_TOKEN|
	--<|

	--:MARK_SOURCE_TOKEN|
		--=HalfHP|[*S:hp^] \ 2
		--=HPTotal|[*S:t-bar1_value] + [*S:t-bar3_value]
		--=EstFinalHP|[$HPTotal] - [$RetaliationDamage]
		--? [$EstFinalHP] -le [$HalfHP] -and [$EstFinalHP] -gt 0|MARK_SOURCE_TOKEN_RED
		--? [$EstFinalHP] -le 0|MARK_SOURCE_TOKEN_DEAD
	--<|

  	--:MARK_SOURCE_TOKEN_RED|
  		--@token-mod| _ids [*S:t-id] _set statusmarkers#red
  	--<|

  	--:MARK_SOURCE_TOKEN_DEAD|
  		--@token-mod| _ids [*S:t-id] _set statusmarkers#dead|-red
  	--<|
}}