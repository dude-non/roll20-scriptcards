!scriptcard{{
	--#title|Zetara Attacks
	--#sourceToken|@{selected|token_id}
	--#targetToken|@{target|token_id}

	--=TargetAC|[*T:ac]
	--? [*T:npc_ac] -lt [$TargetAC]|SkipAC
	--=TargetAC|[*T:npc_ac]
	--:SkipAC|
	--=Level|1
	--? [*S:level] -lt 3|SKIP_EXTRA_LEVELS
	--=Level|2
	--? [*S:level] -lt 5|SKIP_EXTRA_LEVELS
	--=Level|3
	--? [*S:level] -lt 7|SKIP_EXTRA_LEVELS
	--=Level|4
	--? [*S:level] -lt 9|SKIP_EXTRA_LEVELS
	--=Level|5
	--:SKIP_EXTRA_LEVELS|

	--+COVER|[rbutton]Half Cover::HALF_COVER[/rbutton]
	--+COVER|[rbutton]3/4 Cover::3Q_COVER[/rbutton]
	--:COVER_RETURN|

	--+ATTACKS|Weapons
	--+WEAPON|[rbutton]Dagger::DAGGER[/rbutton]
	--+WEAPON|Spear [rbutton]One-Handed::SPEAR;6[/rbutton] [rbutton]Two-Handed::SPEAR;8[/rbutton]
	--+WEAPON|Clockwork Spear [rbutton]One-Handed::CLOCKWORK_SPEAR;6[/rbutton] [rbutton]Two-Handed::CLOCKWORK_SPEAR;8[/rbutton]
	--+WEAPON|[rbutton]Pact Scimitar::PACT_SCIMITAR;n[/rbutton] [rbutton]Momentum::PACT_SCIMITAR;m[/rbutton]
	--+SPELLS|Cantrips
	--+CANTRIP|[rbutton]Green Flame Blade::GREEN_FLAME_BLADE[/rbutton]
	--+CANTRIP|[rbutton]Eldritch Blast::ELDRITCH_BLAST[/rbutton]
	--+SPELLS|Level 1
	--+SPELL|[rbutton]Armor of Agathys Retaliation::ARMOR_OF_AGATHYS[/rbutton]
	--+SPELL|[rbutton]Witch Bolt::WITCH_BOLT[/rbutton]
	--+SPELLS|Level 3
	--+SPELL|[rbutton]Vampiric Touch::VAMPIRIC_TOUCH[/rbutton]

	--:INIT_VALUES|
	--=Damage1Count|0
	--=Damage1Die|0
	--&Damage1Type|NULL
	--=Damage2Count|0
	--=Damage2Die|0
	--&Damage2Type|NULL
	--&SEffectString|[*S:t-bar2_value]
	--&TEffectString|[*T:t-bar2_value]
	--=BardicFlag|0
	--=BardicDie|@{Zailith Blackstaff|BardicInspirationDie}
	--=CritFlag|0
	--=LuckyFlag|0
	--&AttackType|NULL
	--&WeaponType|NULL
	--&WeaponName|NULL
	--=DamageFlag|0
	--=GFBFlag|0
	--=WBFlag|0
	--=VTFlag|0
	--&WeaponTags|NULL
	--&DeflectString|NULL
	--&Handed|NULL
	--=Deflect|0
	--=DeflectFlag|0
	--=DeflectableFlag|0
	--X|

	--:HALF_COVER|
		--+COVER|Half Cover: AC + 2!
		--=TargetAC|[$TargetAC] + 2
	--^COVER_RETURN|

	--:3Q_COVER|
		--+COVER|3/4 Cover: AC + 5!
		--=TargetAC|[$TargetAC] + 5
	--^COVER_RETURN|

	--:DAGGER|
		--=Damage1Count|1
		--=Damage1Die|4ro<1
		--&Damage1Type|Piercing
		--&DamageMod|[*S:dexterity_mod]
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb]
		--&AttackType|DEX
		--&WeaponType|MELEE
		--&WeaponName|Dagger
		-->DEFLECT_STRING|
		--=DeflectableFlag|1
	--^SELECT_ATTACK|

	--:SPEAR|
		--=Damage1Count|1
		--=Damage1Die|[&reentryval]
		--? [$Damage1Die] -eq 6|>ONE_HANDED_SPEAR
		--? [$Damage1Die] -eq 8|>TWO_HANDED_SPEAR
		--&Damage1Type|Piercing
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb]
		--&AttackType|DEX
		--&WeaponType|MELEE
		--&WeaponName|Spear
		-->DEFLECT_STRING|
		--=DeflectableFlag|1
	--^SELECT_ATTACK|

	--:CLOCKWORK_SPEAR|
		--=Damage1Count|1
		--=Damage1Die|[&reentryval]
		--? [$Damage1Die] -eq 6|>ONE_HANDED_SPEAR
		--? [$Damage1Die] -eq 8|>TWO_HANDED_SPEAR
		--&Damage1Type|Magical Piercing
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb]
		--&AttackType|DEX
		--&WeaponType|MELEE
		--&WeaponName|Clockwork Spear
		--? [*S:repeating_resource_$0_resource_right] -lt 1|SELECT_ATTACK
		--+OPTION|Use Clockwork? [rbutton]Yes::CLOCKWORK_SPEAR_ACTIVATE;10[/rbutton] [rbutton]No::SELECT_ATTACK[/rbutton]
		--=DeflectableFlag|1
	--X|

	--:ONE_HANDED_SPEAR|
		--&DamageMod|[*S:dexterity_mod]
	--<|

	--:TWO_HANDED_SPEAR|
		--=VersatileBonus|[*S:dexterity_mod] \ 2
		--&DamageMod|[*S:dexterity_mod] + [$VersatileBonus.Raw]
	--<|

	--:CLOCKWORK_SPEAR_ACTIVATE|
		--@modattr| _charid [*S:id] _repeating_resource_$0_resource_right|-1 _silent
	--^TO_HIT|

	--:PACT_SCIMITAR|
		--=Damage1Count|1
		--? [&reentryval] -eq m|>MOMENTUM_BONUS_DIE
		--=Damage1Die|8
		--&Damage1Type|Magical Slashing
		--&DamageMod|[*S:dexterity_mod]
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb]
		--&AttackType|DEX
		--&WeaponType|MELEE
		--&WeaponName|Pact Scimitar
		--&WeaponTags|Keen
		--&DeflectString|Deflect:[$Damage1Count.Raw]d[$Damage1Die.Raw]+[*S:dexterity_mod]:
		-->DEFLECT_STRING|
		--=DeflectableFlag|1
	--^SELECT_ATTACK|

	--:MOMENTUM_BONUS_DIE|
		--=Damage1Count|2
	--<|

	--:GREEN_FLAME_BLADE|
		--=GFBFlag|1
		--=Damage2Count|0
		--=Damage2Die|0
		--&Damage2Type|Fire
		--? [*S:level] -lt 5|SKIP_EXTRA_GFB
		--=Damage2Count|1
		--=Damage2Die|8
		--? [*S:level] -lt 11|SKIP_EXTRA_GFB
		--=Damage2Count|2
		--? [*S:level] -lt 11|SKIP_EXTRA_GFB
		--=Damage2Count|3
		--:SKIP_EXTRA_GFB|
		--+WEAPON|[rbutton]Dagger::DAGGER[/rbutton]
		--+WEAPON|Spear [rbutton]One-Handed::SPEAR;6[/rbutton] [rbutton]Two-Handed::SPEAR;8[/rbutton]
		--+WEAPON|Clockwork Spear [rbutton]One-Handed::CLOCKWORK_SPEAR;6[/rbutton] [rbutton]Two-Handed::CLOCKWORK_SPEAR;8[/rbutton]
		--+WEAPON|[rbutton]Pact Scimitar::PACT_SCIMITAR[/rbutton]
		--=DeflectableFlag|1
	--X|

	--:ELDRITCH_BLAST|
		--=Damage1Count|1
		--=Damage1Die|10
		--&Damage1Type|Force
		--&DamageMod|0
		--&ToHitMod|[*S:spell_attack_bonus]
		--&AttackType|SPELL
		--&WeaponType|RANGED
		--&WeaponName|Eldritch Blast
		-->DEFLECT_STRING|
		--=DeflectableFlag|0
	--^SELECT_ATTACK|

	--:ARMOR_OF_AGATHYS|
		--=AoA|[$Level] * 5
		--=Damage1Count|[$AoA]
		--=Damage1Die|1
		--=Damage1Type|Cold
		--&AttackType|NONE
		--&WeaponType|NONE
		--&WeaponName|Armor of Agathys
		--=DeflectableFlag|0
	--^DAMAGE|

	--:WITCH_BOLT|
		--=WBFlag|1
		--=Bonus|[$Level] - 1
		--=Damage1Count|1 + [$Bonus]
		--=Damage1Die|12
		--&Damage1Type|Lightning
		--&DamageMod|0
		--&ToHitMod|[*S:spell_attack_bonus]
		--&AttackType|SPELL
		--&WeaponType|RANGED
		--&WeaponName|Witch Bolt
		--? [&SEffectString] -inc WitchBolt|DAMAGE
		--? [*S:lvl[$Level]_slots_expended] -lt 1|NOT_ENOUGH_SLOTS
		--@modattr| _charid [*S:id] _lvl[$Level]_slots_expended|-1 _silent
		-->DEFLECT_STRING|
		--=DeflectableFlag|0
	--^SELECT_ATTACK|

	--:VAMPIRIC_TOUCH|
		--? [&SEffectString] -inc VampiricTouch|SKIP_LEVEL_COST_VT
		--? [*S:lvl[$Level]_slots_expended] -lt 1|NOT_ENOUGH_SLOTS
		--@modattr| _charid [*S:id] _lvl[$Level]_slots_expended|-1 _silent
		--&SEffectString|+VampiricTouch
		--@token-mod| _ids [*S:t-id] _set bar2_value|[&SEffectString]
		--:SKIP_LEVEL_COST_VT|
		--=VTFlag|1
		--=Bonus|[$Level] - 3
		--=Damage1Count|3 + [$Bonus]
		--=Damage1Die|6
		--&Damage1Type|Necrotic
		--&DamageMod|0
		--&ToHitMod|[*S:spell_attack_bonus]
		--&AttackType|SPELL
		--&WeaponType|MELEE
		--&WeaponName|Vampiric Touch
		-->DEFLECT_STRING|
		--=DeflectableFlag|1
	--^SELECT_ATTACK|

	--:DEFLECT_STRING|
		--? "[&SEffectString]" -inc "Deflect"|>REMOVE_DEFLECT
		--? "[&DeflectString]" -eq "NULL"|SKIP_DEFLECT_STRING
		--&SEffectString|+[&DeflectString]
		--:SKIP_DEFLECT_STRING|
		--@token-mod| _ids [*S:t-id] _set bar2_value|[&SEffectString]
	--<|

	--:REMOVE_DEFLECT|
		--~BeforeDeflect|string;before;Deflect:;[&SEffectString]
		--~AfterDeflect|string;after;Deflect:;[&SEffectString]
		--~AfterMarker|string;after;:;[&AfterDeflect]
		--&SEffectString|[&BeforeDeflect][&AfterMarker]
	--<|

	--:SELECT_ATTACK|
		--? [&SEffectString] -inc Invisible|>INVIS_EMOTE
		--? [&SEffectString] -ninc Invisible|>NORMAL_EMOTE
		--+ATTACK|[rbutton]Advantage::TO_HIT;2d20kh1[/rbutton]
		--+ATTACK|[rbutton]Normal::TO_HIT;1d20[/rbutton]
		--+ATTACK|[rbutton]Disadvantage::TO_HIT;2d20kl1[/rbutton]
	--X|

	--:INVIS_EMOTE|
		--#emoteText|Something attacks @{target|token_name} with ...something?
	--<|

	--:NORMAL_EMOTE|
		--#emoteText|@{selected|token_name} attacks @{target|token_name} with their [&WeaponName]
	--<|

	--:TO_HIT|
		--&SEffectString|[*S:t-bar2_value]
		--&TEffectString|[*T:t-bar2_value]	
		--&RollString|[&reentryval] + [&ToHitMod]

		--? [&SEffectString] -inc Bless|>BLESS
		--? [&SEffectString] -inc Bane|>BANE
		--? [&SEffectString] -inc BolsteringMagic|>BOLSTERING_MAGIC

		--+ROLL|[&RollString]
		--=ToHit|[&RollString]
		--+TO HIT|[$ToHit]

		--:LUCKY_RETURN|
		--=CritFlag|0
		--? [$ToHit.Base] -eq 20|CRITICAL
		--? [$ToHit.Base] -eq 1|FUMBLE
		--:BARDIC_RETURN|
		--? [$ToHit] -ge [$TargetAC]|HIT
		--? [$ToHit] -lt [$TargetAC]|MISS
	--X|

	--:BLESS|
 		--=Bless|1d4
 		--+Bless|[$Bless]
 		--&RollString|+ + [$Bless.Raw]
	--<|

	--:BANE|
 		--=Bane|1d4
 		--+Bane|[$Bane]
 		--&RollString|+ - [$Bane.Raw]
	--<|

	--:BOLSTERING_MAGIC|
 		--=Bolster|1d3
 		--+Bolstering Magic|[$Bolster]
 		--&RollString|+ + [$Bolster.Raw]
	--<|

	--:CRITICAL|
		--? [&TEffectString] -inc CritNegate|NEGATE_CRIT
		--=CritBonus|[$Damage1Count.Raw] * [$Damage1Die.Raw]
		--&DamageMod|+ + [$CritBonus] [CRIT]
		--=Damage2Count|[$Damage2Count] * 2
		--? [&WeaponTags] -inc Keen|>KEEN_CRIT
		--+CRITICAL|[rbutton]Critical Hit!::DAMAGE[/rbutton]
		--=CritFlag|1
		--? [&TEffectString] -inc Deflect|>PROMPT_DEFLECT
	--X|

	--:KEEN_CRIT|
		--=Damage1Count|[$Damage1Count] + 1
	--<|

	--:NEGATE_CRIT|
		--+CRITICAL|Critical Hit!
		--+NEGATED|Critical Negated!
		--+HIT|[rbutton]Hit!::DAMAGE[/rbutton]
	--X|

	--:FUMBLE|
		--+FUMBLE|Critical Failure.
		--? [*S:repeating_resource_$0_resource_left] -lt 1|SKIP_LUCKY_FUMBLE
		--? [$LuckyFlag] -eq 1|SKIP_LUCKY_FUMBLE
		--+LUCKY|[rbutton]Use Lucky?::LUCKY[/rbutton]
		--:SKIP_LUCKY_FUMBLE|
		--? [&TEffectString] -inc Deflect|>PROMPT_DEFLECT
	--X|

	--:HIT|
		--? [$WBFlag] -eq 1|>APPLY_WITCH_BOLT
		--+HIT|[rbutton]Hit!::DAMAGE[/rbutton]
		--? [&TEffectString] -inc Deflect|>PROMPT_DEFLECT
	--X|

	--:APPLY_WITCH_BOLT|
		--? [&SEffectString] -inc WitchBolt|SKIP_APPLY_WITCH_BOLT
		--&SEffectString|+WitchBolt
		--@token-mod| _ids [*S:t-id] _set bar2_value|[&SEffectString]
		--:SKIP_APPLY_WITCH_BOLT|
	--<|

	--:MISS|
		--+MISS|Miss.
		--? [&SEffectString] -ninc BardicInspiration|SKIP_BARDIC
		--? [$BardicFlag] -eq 1|SKIP_BARDIC
		--+INSPIRED|[rbutton]Use Bardic Inspiration?::BARDIC_INSPIRATION[/rbutton]
		--:SKIP_BARDIC|
		--? [*S:repeating_resource_$0_resource_left] -lt 1|SKIP_LUCKY_MISS
		--? [$LuckyFlag] -eq 1|SKIP_LUCKY_MISS
		--+LUCKY|[rbutton]Use Lucky?::LUCKY[/rbutton]
		--:SKIP_LUCKY_MISS|
	--X| 

	--:PROMPT_DEFLECT|
		--? [$DeflectableFlag] -eq 0|SKIP_PROMPT_DEFLECT
		--? [$DeflectFlag] -eq 1|SKIP_PROMPT_DEFLECT
		--+DEFLECT|[rbutton]Use Deflection?::DEFLECT[/rbutton]
		--:SKIP_PROMPT_DEFLECT|
	--<|

	--:DEFLECT|		
		--=DeflectFlag|1
		--~AfterDeflect|string;after;Deflect:;[&TEffectString]
		--~DeflectString|string;before;:;[&AfterDeflect]
		--=Deflect|[&DeflectString]
		--+DEFLECT|[$Deflect]
	--^DAMAGE|

	--:BARDIC_INSPIRATION|
		--? [$BardicFlag] -eq 1|ERROR_BARDIC
		--=BardicFlag|1
		--=Bardic|1d[$BardicDie.Raw]
		--+Bardic Roll|[$Bardic]
		--=ToHit|[$ToHit] + [$Bardic]
		--+New To Hit|[$ToHit]

		--~Before|string;before;BardicInspiration;[&SEffectString]
		--~After|string;after;BardicInspiration;[&SEffectString]

		--&SEffectString|[&Before][&After]
		--@token-mod| _ids [*S:t-id] _set bar2_value|[&SEffectString]
	--^BARDIC_RETURN|

	--:LUCKY|
		--@modattr| _charid [*S:id] _repeating_resource_$0_resource_left|-1 _silent
		--=LuckyFlag|1

		--? "[&RollString]" -inc "2d20kh1"|>REMOVE_ADV
		--? "[&RollString]" -inc "2d20kl1"|>REMOVE_DISADV

		--=ToHit|[&RollString]
		--+NEW TO HIT|[$ToHit]
	--^LUCKY_RETURN|

	--:REMOVE_ADV|
		--~After|string;after;2d20kh1;[&RollString]

		--&RollString|1d20[&After]
	--<|

	--:REMOVE_DISADV|
		--~After|string;after;2d20kl1;[&RollString]

		--&RollString|1d20[&After]
	--<|

	--:DAMAGE|		
		--&SEffectString|[*S:t-bar2_value]
		--&TEffectString|[*T:t-bar2_value]	
		--? [$DamageFlag] -eq 1|ERROR_DAMAGE
		--=DamageFlag|1
		--=Damage1|0
		--=Damage2|0
		--=EMDamage|0

		--&Damage1String|[$Damage1Count.Raw]d[$Damage1Die.Raw]
		--? [$Damage2Count] -eq 0|SkipDamage2a
		--&Damage2String|[$Damage2Count.Raw]d[$Damage2Die.Raw]
		--:SkipDamage2a|
		--? [$GFBFlag] -eq 0|SKIP_GFB_DAMAGE
		--&GFBDamageString|[$Damage2Count.Raw]d[$Damage2Die.Raw] + [*S:charisma_mod]
		--:SKIP_GFB_DAMAGE|
		
		--? "[&WeaponTags]" -inc "Balanced"|>ADD_BALANCED

		--? [&SEffectString] -inc Enlarged|>ENLARGED
		--? [&SEffectString] -inc Reduced|>REDUCED

		--<|

		--&Damage1String|+ + [&DamageMod]

		--=Damage1|[&Damage1String]
		--+[&Damage1Type] DAMAGE|[$Damage1]
		--? [$Damage2Count] -eq 0|SkipDamage2b
		--=Damage2|[&Damage2String]
		--+[&Damage2Type] DAMAGE|[$Damage2]
		--:SkipDamage2b|
		--? [$GFBFlag] -eq 0|SKIP_GFB_DAMAGE_ROLL
		--=AdjacentDamage|[&GFBDamageString]
		--+ADJACENT DAMAGE|[$AdjacentDamage]
		--:SKIP_GFB_DAMAGE_ROLL|

		--? [&SEffectString] -inc Enfeebled|>ENFEEBLED
		--? [&SEffectString] -inc EldritchMaul|>ELDRITCH_MAUL

		--? "[*T:npc_vulnerabilities]" -inc "[&Damage1Type]"|>VULN_1
		--? "[*T:npc_vulnerabilities]" -inc "[&Damage2Type]"|>VULN_2
		--? "[*T:npc_resistances]" -inc "[&Damage1Type]"|>RESIST_1
		--? "[*T:npc_resistances]" -inc "[&Damage2Type]"|>RESIST_2
		--? "[*T:npc_immunities]" -inc "[&Damage1Type]"|>IMMUNE_1
		--? "[*T:npc_immunities]" -inc "[&Damage2Type]"|>IMMUNE_2

		--? [&TEffectString] -inc Underwater|>UNDERWATER

		--=TotalDamage|[$Damage1] + [$Damage2] + [$EMDamage] - [$Deflect.Raw]

		--? "[&TEffectString]" -inc "DT"|>APPLY_DAMAGE_THRESHOLD

		--+TotalDamage|[$TotalDamage]

		--? [&TEffectString] -inc Retaliation|>RETALIATION

		--? [$VTFlag] -eq 1|>APPLY_VT_HEALING
	--^APPLY_DAMAGE|

	--:APPLY_VT_HEALING|
		--=Healing|[$Damage1] \ 2
		--@alter|_target|@{selected|token_id} _bar|1 _amount|[$Healing]
	--<|

	--:ADD_PUNCTURE|
		--&Damage1String|+!
	--<|

	--:ADD_BALANCED|
		--&Damage1String|[&Damage1String]ro<1
		--+TEST|[&Damage1String]
	--<|

	--:ENLARGED|
		--? [&AttackType] -ne STR|ENLARGED_SKIP
		--? [$CritFlag] -eq 1|ENLARGED_CRIT
		--&Damage1String|+ + 1d4
		--:ENLARGED_CRIT_RETURN|
		--:ENLARGED_SKIP|
	--<|

	--:ENLARGED_CRIT|
		--&Damage1String|+ + 2d4
	--^ENLARGED_CRIT_RETURN|

	--:REDUCED|
		--? [&AttackType] -eq NONE|SKIP_REDUCED
		--&Damage1String|+ - 1d4
		--:SKIP_REDUCED|
	--<|
	
	--:ENFEEBLED|
		--? [&AttackType] -ne STR|SKIP_ENFEEBLED
		--+ENFEEBLED|Damage Halved!
		--=Damage1|[$Damage1] \ 2
		--:SKIP_ENFEEBLED|
	--<|

	--:ELDRITCH_MAUL|
		--? [&WeaponType] -ne MELEE|SKIP_ELDRITCH_MAUL
		--? [$CritFlag] -eq 1|>EM_CRIT
		--? [$CritFlag] -eq 0|>EM_DAMAGE
		--? "[*T:npc_vulnerabilities]" -inc "Force"|>EM_VULN
		--? "[*T:npc_resistances]" -inc "Force"|>EM_RESIST
		--? "[*T:npc_immunities]" -inc "Force"|>EM_IMMUNE
		--+Force DAMAGE|[$EMDamage]
		--:SKIP_ELDRITCH_MAUL|
	--<|

	--:EM_CRIT|
		--=EMDamage|2d6
	--<|

	--:EM_DAMAGE|
		--=EMDamage|1d6
	--<|

	--:VULN_1|
		--=Damage1|[$Damage1] * 2
	--<|

	--:VULN_2|
		--=Damage2|[$Damage2] * 2
	--<|

	--:EM_VULN|
		--=EMDamage|[$EMDamage] * 2
	--<|

	--:RESIST_1|
		--=Damage1|[$Damage1] \ 2
	--<|

	--:RESIST_2|
		--=Damage2|[$Damage2] \ 2
	--<|

	--:EM_RESIST|
		--=EMDamage|[$EMDamage] \ 2
	--<|

	--:IMMUNE_1|
		--=Damage1|0
	--<|

	--:IMMUNE_2|
		--=Damage2|0
	--<|

	--:EM_IMMUNE|
		--=EMDamage|0
	--<|

	--:UNDERWATER|
		--? "[*T:npc_resistances]" -inc "Fire"|SKIP_UNDERWATER
		--? [&Damage1Type] -eq Fire|>RESIST_1
		--? [&Damage2Type] -eq Fire|>RESIST_2
		--:SKIP_UNDERWATER|
	--<|

	--:APPLY_DAMAGE_THRESHOLD|
		--+DT|Applying Damage Threshold...

		--~AfterString|string;after;DT;[&EffectString2]
		--~DT|string;left;2;[&AfterString]
		--=DT|[&DT]

		--? [$TotalDamage] -le [$DT]|[
			--=TotalDamage|0
			--+Resisted|@{target|character_name} Resisted the Attack!
		--]|
	--<|

	--:APPLY_DAMAGE|
		--? @{target|bar3} -gt 0|APPLY_TEMP_DAMAGE
		--@alter|_target|@{target|token_id} _bar|1 _amount|-[$TotalDamage]
		--^MARK_TOKEN|

	--:APPLY_TEMP_DAMAGE|
		--? @{target|bar3} -lt [$TotalDamage]|APPLY_TEMP_PARTIAL
		--@alter|_target|@{target|token_id} _bar|3 _amount|-[$TotalDamage]
		--^MARK_TOKEN|

	--:APPLY_TEMP_PARTIAL|
		--=RemainderDamage|[$TotalDamage] - @{target|bar3}
		--@alter|_target|@{target|token_id} _bar|3 _amount|-@{target|bar3}
		--@alter|_target|@{target|token_id} _bar|1 _amount|-[$RemainderDamage]
		--^MARK_TOKEN|

	--:MARK_TOKEN|
		--=HalfHP|[*T:hp^] \ 2
		--=HPTotal|@{target|bar1} + @{target|bar3}
		--=EstFinalHP|[$HPTotal] - [$TotalDamage]
		--? [$EstFinalHP] -le [$HalfHP] -and [$EstFinalHP] -gt 0|MARK_TOKEN_RED
		--? [$EstFinalHP] -le 0|MARK_TOKEN_DEAD
	--X|

  	--:MARK_TOKEN_RED|
  		--@token-mod| _ids [*T:t-id] _ignore-selected _set statusmarkers#red
  	--X|

  	--:MARK_TOKEN_DEAD|
  		--@token-mod| _ids [*T:t-id] _ignore-selected _set statusmarkers#dead|-red
  	--X|

  	--:ERROR_BARDIC|
  		--+ERROR|You have already used Bardic Inspiration!
  	--X|

  	--:ERROR_DAMAGE|
  		--+ERROR|You have already applied Damage!
  	--X|

  	--:NOT_ENOUGH_SLOTS|
  		--+ERROR|Not Enough Level [$Level] Slots to Cast!
  	--X|

  	--:RETALIATION|
  		--=RetaliationFlag|1

  		--~AfterMark|string;after;Retaliation:;[&TEffectString]
  		--~AfterMark2|string;after;:;[&AfterMark]
  		--~RetaliationDamageString|string;before;:;[&AfterMark]
  		--~RetaliationDamageType|string;before;:;[&AfterMark2]

  		--=RetaliationDamage|[&RetaliationDamageString]
  		
  		--? [&TEffectString] -inc TempHealthRetaliation|>TEMP_HEALTH_RETALIATION
  		--? [&TEffectString] -inc MeleeRetaliation|>MELEE_RETALIATION
  		--? [&TEffectString] -inc DistanceRetaliation|>DISTANCE_RETALITION

  		--? [$RetaliationFlag] -eq 0|SKIP_RETALIATION

		--? "[*S:npc_vulnerabilities]" -inc "[&RetaliationDamageType]"|>VULN_RETLATIATION
		--? "[*S:npc_resistances]" -inc "[&RetaliationDamageType]"|>RESIST_RETLATIATION
		--? "[*S:npc_immunities]" -inc "[&RetaliationDamageType]"|>IMMUNE_RETLATIATION

		--+RETALIATION|[$RetaliationDamage] [&RetaliationDamageType] Damage

		--? [*S:t-bar3_value] -eq 0|>RETALIATION_APPLY_DAMAGE
		--? [*S:t-bar3_value] -le [$RetaliationDamage]|>RETALIATION_APPLY_PARTIAL_DAMAGE
		--? [*S:t-bar3_value] -gt [$RetaliationDamage]|>RETALIATION_APPLY_TEMP_DAMAGE

		--:SKIP_RETALIATION|
  	--<|

  	--:TEMP_HEALTH_RETALIATION|
  		--? [&WeaponType] -eq RANGED|>INVALID_RETALIATION
  		--? [$RetaliationDamage] -ge [*T:t-bar3_value]|>TEMP_HEALTH_RETALIATION_REMOVE
  	--<|

  	--:MELEE_RETALIATION|
  		--? [&WeaponType] -eq RANGED|>INVALID_RETALIATION
  	--<|

  	--:DISTANCE_RETALITION|
  		--? [&WeaponType] -eq RANGED|>INVALID_RETALIATION

  		--~AfterMark3|string;after;:;[&AfterMark2]
  		--~MaxDistance|string;before;:;[&AfterMark3]
  		--=MaxDistance|[&MaxDistance]

  		--? [$Distance] -gt [$MaxDistance]|>INVALID_RETALIATION
  	--<|

	--:VULN_RETLATIATION|
		--=RetaliationDamage|[$RetaliationDamage] * 2
	--<|

	--:RESIST_RETLATIATION|
		--=RetaliationDamage|[$RetaliationDamage] \ 2
	--<|

	--:IMMUNE_RETLATIATION|
		--=RetaliationDamage|0
	--<|

	--:INVALID_RETALIATION|
		--=RetaliationFlag|0
	--<|

	--:TEMP_HEALTH_RETALIATION_REMOVE|
		--+TEST|REMOVE
		--~BeforeRetaliation|string;before;TempHealthRetaliation;[&TEffectString]
		--~AfterRetaliation|string;after;:;[&AfterMark2]

		--&TEffectString|[&BeforeRetaliation][&AfterRetaliation]

		--@token-mod| _ids [*T:t-id] _set bar2_value|[&TEffectString]
	--<|

	--:RETALIATION_APPLY_DAMAGE|
		--@alter|_target|@{selected|token_id} _bar|1 _amount|-[$RetaliationDamage]
		--^MARK_SOURCE_TOKEN|
	--<|

	--:RETALIATION_APPLY_PARTIAL_DAMAGE|
		--=RemainderDamage|[$RetaliationDamage] - [*S:t-bar3_value]
		--@alter|_target|@{selected|token_id} _bar|3 _amount|-[*S:t-bar3_value]
		--@alter|_target|@{selected|token_id} _bar|1 _amount|-[$RemainderDamage]
		--^MARK_SOURCE_TOKEN|
	--<|

	--:RETALIATION_APPLY_TEMP_DAMAGE|
		--@alter|_target|@{selected|token_id} _bar|3 _amount|-[$RetaliationDamage]
		--^MARK_SOURCE_TOKEN|
	--<|

	--:MARK_SOURCE_TOKEN|
		--=HalfHP|[*S:hp^] \ 2
		--=HPTotal|[*S:t-bar1_value] + [*S:t-bar3_value]
		--=EstFinalHP|[$HPTotal] - [$RetaliationDamage]
		--? [$EstFinalHP] -le [$HalfHP] -and [$EstFinalHP] -gt 0|MARK_SOURCE_TOKEN_RED
		--? [$EstFinalHP] -le 0|MARK_SOURCE_TOKEN_DEAD
	--<|

  	--:MARK_SOURCE_TOKEN_RED|
  		--@token-mod| _ids [*S:t-id] _set statusmarkers#red
  	--<|

  	--:MARK_SOURCE_TOKEN_DEAD|
  		--@token-mod| _ids [*S:t-id] _set statusmarkers#dead|-red
  	--<|

}}