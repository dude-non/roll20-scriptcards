!scriptcard{{
	--#title|Zailith Attacks
	--#sourceToken|@{selected|token_id}
	--#targetToken|@{target|token_id}

	--=TargetAC|[*T:ac]
	--? [*T:npc_ac] -lt [$TargetAC]|SkipAC
	--=TargetAC|[*T:npc_ac]
	--:SkipAC|

	--+COVER|[rbutton]Half Cover::HALF_COVER[/rbutton]
	--+COVER|[rbutton]3/4 Cover::3Q_COVER[/rbutton]
	--:COVER_RETURN|

	--+WEAPON|Zailith's Rapier [rbutton]Stab::ZAILITHS_RAPIER;Piercing[/rbutton] [rbutton]Smack::ZAILITHS_RAPIER;Bludgeoning[/rbutton]
	--+WEAPON|[rbutton]Admiral's Trusty Dagger::ADMIRALS_TRUSTY_DAGGER[/rbutton]
	--+WEAPON|[rbutton]Dagger::DAGGER[/rbutton]
	--+WEAPON|[rbutton]Flintlock Pistol::FLINTLOCK_PISTOL[/rbutton]
	--+CANTRIP|[rbutton]Shillelagh::SHILLELAGH[/rbutton]
	--X|

	--:INIT_VALUES|
	--=Damage1Count|0
	--=Damage1Die|0
	--&Damage1Type|NULL
	--=Damage2Count|0
	--=Damage2Die|0
	--&Damage2Type|NULL
	--&SEffectString|[*S:t-bar2_value]
	--&TEffectString|[*T:t-bar2_value]
	--=BardicFlag|0
	--=BardicDie|@{Nasmyry|BardicInspirationDie}
	--=CritFlag|0
	--&AttackType|NULL
	--&WeaponName|NULL
	--=DamageFlag|0
	--&WeaponTags|NULL
	--=Deflect|0
	--=DeflectFlag|0
	--=DeflectableFlag|0
	--X|

	--:HALF_COVER|
		--+COVER|Half Cover: AC + 2!
		--=TargetAC|[$TargetAC] + 2
	--^COVER_RETURN|

	--:3Q_COVER|
		--+COVER|3/4 Cover: AC + 5!
		--=TargetAC|[$TargetAC] + 5
	--^COVER_RETURN|

	--:ZAILITHS_RAPIER|
		--=Damage1Count|1
		--=Damage1Die|8
		--&Damage1Type|[&reentryval]
		--&DamageMod|[*S:dexterity_mod]
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb]
		--&AttackType|DEX
		--&WeaponName|Zailith's Rapier
		--&WeaponTags|Puncture
		--=DeflectableFlag|1
	--^SELECT_ATTACK|

	--:ADMIRALS_TRUSTY_DAGGER|
		--=Damage1Count|1
		--=Damage1Die|4
		--&Damage1Type|Magical Piercing
		--&DamageMod|[*S:dexterity_mod] + 1
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb] + 1
		--&AttackType|DEX
		--&WeaponName|Admiral's Trusty Dagger
		--&WeaponTags|Balanced
		--=DeflectableFlag|1
	--^SELECT_ATTACK|

	--:DAGGER|
		--=Damage1Count|1
		--=Damage1Die|4
		--&Damage1Type|Piercing
		--&DamageMod|[*S:dexterity_mod]
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb]
		--&AttackType|DEX
		--&WeaponName|Dagger
		--&WeaponTags|Balanced
		--=DeflectableFlag|1
	--^SELECT_ATTACK|

	--:FLINTLOCK_PISTOL|
		--? [*S:other_resource_$0_resource_right] -lt 1|NOT_ENOUGH_AMMO
		--? [*S:other_resource_$1_resource_left] -lt 1|NOT_ENOUGH_AMMO
		--@modattr| _charid [*S:id] _other_resource_$0_resource_right|-1 _silent
		--@modattr| _charid [*S:id] _other_resource_$1_resource_left|-1 _silent
		--=Damage1Count|2
		--=Damage1Die|6
		--&Damage1Type|Bludgeoning
		--&DamageMod|[*S:dexterity_mod]
		--&ToHitMod|[*S:dexterity_mod]
		--&AttackType|DEX
		-_&WeaponName|Flintlock Pistol
		--&WeaponTags|Puncture
		--=DeflectableFlag|0
	--^SELECT_ATTACK|

	--:SHILLELAGH|
		--=Damage1Count|1
		--=Damage1Die|8
		--&Damage1Type|Magical Bludgeoning
		--&DamageMod|[*S:charisma_mod]
		--&ToHitMod|[*S:charisma_mod] + [*S:pb]
		--&AttackType|SPELL
		--&WeaponName|Shillelagh
		--=DeflectableFlag|1
	--^SELECT_ATTACK|

	--:SELECT_ATTACK|
		--? [&SEffectString] -inc Invisible|>INVIS_EMOTE
		--? [&SEffectString] -ninc Invisible|>NORMAL_EMOTE
		--+ATTACK|[rbutton]Advantage::TO_HIT;2d20kh1[/rbutton]
		--+ATTACK|[rbutton]Normal::TO_HIT;1d20[/rbutton]
		--+ATTACK|[rbutton]Disadvantage::TO_HIT;2d20kl1[/rbutton]
	--X|

	--:INVIS_EMOTE|
		--#emoteText|Something attacks @{target|token_name} with ...something?
	--<|

	--:NORMAL_EMOTE|
		--#emoteText|@{selected|token_name} attacks @{target|token_name} with their [&WeaponName]
	--<|

	--:TO_HIT|
		--&SEffectString|[*S:t-bar2_value]
		--&TEffectString|[*T:t-bar2_value]	
		--&RollString|[&reentryval] + [&ToHitMod]

		--? [&SEffectString] -inc Bless|>BLESS
		--? [&SEffectString] -inc Bane|>BANE
		--? [&SEffectString] -inc BolsteringMagic|>BOLSTERING_MAGIC

		--+ROLL|[&RollString]
		--=ToHit|[&RollString]
		--+TO HIT|[$ToHit]

		--? [$ToHit.Base] -eq 20|CRITICAL
		--? [$ToHit.Base] -eq 1|FUMBLE
		--:BARDIC_RETURN|
		--? [$ToHit] -ge [$TargetAC]|HIT
		--? [$ToHit] -lt [$TargetAC]|MISS
	--X|

	--:BLESS|
 		--=Bless|1d4
 		--+Bless|[$Bless]
 		--&RollString|+ + [$Bless.Raw]
	--<|

	--:BANE|
 		--=Bane|1d4
 		--+Bane|[$Bane]
 		--&RollString|+ - [$Bane.Raw]
	--<|

	--:BOLSTERING_MAGIC|
 		--=Bolster|1d3
 		--+Bolstering Magic|[$Bolster]
 		--&RollString|+ + [$Bolster.Raw]
	--<|

	--:CRITICAL|
		--? [&TEffectString] -inc CritNegate|NEGATE_CRIT
		--=CritBonus|[$Damage1Count.Raw] * [$Damage1Die.Raw]
		--&DamageMod|+ + [$CritBonus] [CRIT]
		--=Damage2Count|[$Damage2Count] * 2
		--+CRITICAL|[rbutton]Critical Hit!::DAMAGE[/rbutton]
		--=CritFlag|1
		--? [&TEffectString] -inc Deflect|>PROMPT_DEFLECT
	--X|

	--:NEGATE_CRIT|
		--+CRITICAL|Critical Hit!
		--+NEGATED|Critical Negated!
		--+HIT|[rbutton]Hit!::DAMAGE[/rbutton]
		--? [&TEffectString] -inc Deflect|>PROMPT_DEFLECT
	--X|

	--:FUMBLE|
		--+FUMBLE|Critical Failure.
	--X|

	--:HIT|
		--+HIT|[rbutton]Hit!::DAMAGE[/rbutton]
		--? [&TEffectString] -inc Deflect|>PROMPT_DEFLECT
	--X|

	--:MISS|
		--+MISS|Miss.
		--? [&SEffectString] -ninc BardicInspiration|SKIP_BARDIC
		--? [$BardicFlag] -eq 1|SKIP_BARDIC
		--+INSPIRED|[rbutton]Use Bardic Inspiration?::BARDIC_INSPIRATION[/rbutton]
		--:SKIP_BARDIC|
	--X| 

	--:PROMPT_DEFLECT|
		--? [$DeflectableFlag] -eq 0|SKIP_PROMPT_DEFLECT
		--? [$DeflectFlag] -eq 1|SKIP_PROMPT_DEFLECT
		--+DEFLECT|[rbutton]Use Deflection?::DEFLECT[/rbutton]
		--:SKIP_PROMPT_DEFLECT|
	--<|

	--:DEFLECT|		
		--=DeflectFlag|1
		--~AfterDeflect|string;after;Deflect:;[&TEffectString]
		--~DeflectString|string;before;:;[&AfterDeflect]
		--=Deflect|[&DeflectString]
		--+DEFLECT|[$Deflect]
	--^DAMAGE|

	--:BARDIC_INSPIRATION|
		--? [$BardicFlag] -eq 1|ERROR_BARDIC
		--=BardicFlag|1
		--=Bardic|1d[$BardicDie.Raw]
		--+Bardic Roll|[$Bardic]
		--=ToHit|[$ToHit] + [$Bardic]
		--+New To Hit|[$ToHit]

		--~Before|string;before;BardicInspiration;[&SEffectString]
		--~After|string;after;BardicInspiration;[&SEffectString]

		--&SEffectString|[&Before][&After]
		--@token-mod| _ids [*S:t-id] _set bar2_value|[&SEffectString]
	--^BARDIC_RETURN|

	--:DAMAGE|		
		--&SEffectString|[*S:t-bar2_value]
		--&TEffectString|[*T:t-bar2_value]	
		--? [$DamageFlag] -eq 1|ERROR_DAMAGE
		--=DamageFlag|1
		--=Damage1|0
		--=Damage2|0

		--&Damage1String|[$Damage1Count.Raw]d[$Damage1Die.Raw]
		--? [$Damage2Count] -eq 0|SkipDamage2a
		--&Damage2String|[$Damage2Count.Raw]d[$Damage2Die.Raw]
		--:SkipDamage2a|
		
		--? "[&WeaponTags]" -inc "Puncture"|>ADD_PUNCTURE
		--? "[&WeaponTags]" -inc "Balanced"|>ADD_BALANCED

		--? [&SEffectString] -inc Enlarged|>ENLARGED
		--? [&SEffectString] -inc Reduced|>REDUCED

		--&Damage1String|+ + [&DamageMod]

		--=Damage1|[&Damage1String]
		--+[&Damage1Type] Damage|[$Damage1]
		--? [$Damage2Count] -eq 0|SkipDamage2b
		--=Damage2|[&Damage2String]
		--+[&Damage2Type] Damage|[$Damage2]
		--:SkipDamage2b|

		--? [&SEffectString] -inc Enfeebled|>ENFEEBLED

		--? "[*T:npc_vulnerabilities]" -inc "[&Damage1Type]"|>VULN_1
		--? "[*T:npc_vulnerabilities]" -inc "[&Damage2Type]"|>VULN_2
		--? "[*T:npc_resistances]" -inc "[&Damage1Type]"|>RESIST_1
		--? "[*T:npc_resistances]" -inc "[&Damage2Type]"|>RESIST_2
		--? "[*T:npc_immunities]" -inc "[&Damage1Type]"|>IMMUNE_1
		--? "[*T:npc_immunities]" -inc "[&Damage2Type]"|>IMMUNE_2

		--? [&TEffectString] -inc Underwater|>UNDERWATER

		--=TotalDamage|[$Damage1] + [$Damage2] - [$Deflect.Raw]

		--? "[&TEffectString]" -inc "DT"|>APPLY_DAMAGE_THRESHOLD

		--+TotalDamage|[$TotalDamage]

		--? [&TEffectString] -inc Retaliation|>RETALIATION
	--^APPLY_DAMAGE|

	--:ADD_PUNCTURE|
		--&Damage1String|+!
	--<|

	--:ADD_BALANCED|
		--&Damage1String|[&Damage1String]ro<1
		--+TEST|[&Damage1String]
	--<|

	--:ENLARGED|
		--? [&AttackType] -ne STR|ENLARGED_SKIP
		--? [$CritFlag] -eq 1|ENLARGED_CRIT
		--&Damage1String|+ + 1d4
		--:ENLARGED_CRIT_RETURN|
		--:ENLARGED_SKIP|
	--<|

	--:ENLARGED_CRIT|
		--&Damage1String|+ + 2d4
	--^ENLARGED_CRIT_RETURN|

	--:REDUCED|
		--&Damage1String|+ - 1d4
	--<|
	
	--:ENFEEBLED|
		--? [&AttackType] -ne STR|SKIP_ENFEEBLED
		--+ENFEEBLED|Damage Halved!
		--=Damage1|[$Damage1] \ 2
		--:SKIP_ENFEEBLED|
	--<|

	--:VULN_1|
		--=Damage1|[$Damage1] * 2
	--<|

	--:VULN_2|
		--=Damage2|[$Damage2] * 2
	--<|

	--:RESIST_1|
		--=Damage1|[$Damage1] \ 2
	--<|

	--:RESIST_2|
		--=Damage2|[$Damage2] \ 2
	--<|

	--:IMMUNE_1|
		--=Damage1|0
	--<|

	--:IMMUNE_2|
		--=Damage2|0
	--<|

	--:UNDERWATER|
		--? "[*T:npc_resistances]" -inc "Fire"|SKIP_UNDERWATER
		--? [&Damage1Type] -eq Fire|>RESIST_1
		--? [&Damage2Type] -eq Fire|>RESIST_2
		--:SKIP_UNDERWATER|
	--<|

	--:APPLY_DAMAGE_THRESHOLD|
		--+DT|Applying Damage Threshold...

		--~AfterString|string;after;DT;[&EffectString2]
		--~DT|string;left;2;[&AfterString]
		--=DT|[&DT]

		--? [$TotalDamage] -le [$DT]|[
			--=TotalDamage|0
			--+Resisted|@{target|character_name} Resisted the Attack!
		--]|
	--<|

	--:APPLY_DAMAGE|
		--? @{target|bar3} -gt 0|APPLY_TEMP_DAMAGE
		--@alter|_target|@{target|token_id} _bar|1 _amount|-[$TotalDamage]
		--^MARK_TOKEN|

	--:APPLY_TEMP_DAMAGE|
		--? @{target|bar3} -lt [$TotalDamage]|APPLY_TEMP_PARTIAL
		--@alter|_target|@{target|token_id} _bar|3 _amount|-[$TotalDamage]
		--^MARK_TOKEN|

	--:APPLY_TEMP_PARTIAL|
		--=RemainderDamage|[$TotalDamage] - @{target|bar3}
		--@alter|_target|@{target|token_id} _bar|3 _amount|-@{target|bar3}
		--@alter|_target|@{target|token_id} _bar|1 _amount|-[$RemainderDamage]
		--^MARK_TOKEN|

	--:MARK_TOKEN|
		--=HalfHP|[*T:hp^] \ 2
		--=HPTotal|@{target|bar1} + @{target|bar3}
		--=EstFinalHP|[$HPTotal] - [$TotalDamage]
		--? [$EstFinalHP] -le [$HalfHP] -and [$EstFinalHP] -gt 0|MARK_TOKEN_RED
		--? [$EstFinalHP] -le 0|MARK_TOKEN_DEAD
	--X|

  	--:MARK_TOKEN_RED|
  		--@token-mod| _ids [*T:t-id] _ignore-selected _set statusmarkers#red
  	--X|

  	--:MARK_TOKEN_DEAD|
  		--@token-mod| _ids [*T:t-id] _ignore-selected _set statusmarkers#dead|-red
  	--X|

  	--:ERROR_BARDIC|
  		--+ERROR|You have already used Bardic Inspiration!
  	--X|

  	--:ERROR_DAMAGE|
  		--+ERROR|You have already applied Damage!
  	--X|

  	--:NOT_ENOUGH_AMMO|
  		--+ERROR|Not Enough Ammunition!
  	--X|

  	--:RETALIATION|
  		--=RetaliationFlag|1

  		--~AfterMark|string;after;Retaliation:;[&TEffectString]
  		--~AfterMark2|string;after;:;[&AfterMark]
  		--~RetaliationDamageString|string;before;:;[&AfterMark]
  		--~RetaliationDamageType|string;before;:;[&AfterMark2]

  		--=RetaliationDamage|[&RetaliationDamageString]
  		
  		--? [&TEffectString] -inc TempHealthRetaliation|>TEMP_HEALTH_RETALIATION
  		--? [&TEffectString] -inc MeleeRetaliation|>MELEE_RETALIATION
  		--? [&TEffectString] -inc DistanceRetaliation|>DISTANCE_RETALITION

  		--? [$RetaliationFlag] -eq 0|SKIP_RETALIATION

		--? "[*S:npc_vulnerabilities]" -inc "[&RetaliationDamageType]"|>VULN_RETLATIATION
		--? "[*S:npc_resistances]" -inc "[&RetaliationDamageType]"|>RESIST_RETLATIATION
		--? "[*S:npc_immunities]" -inc "[&RetaliationDamageType]"|>IMMUNE_RETLATIATION

		--+RETALIATION|[$RetaliationDamage] [&RetaliationDamageType] Damage

		--? [*S:t-bar3_value] -eq 0|>RETALIATION_APPLY_DAMAGE
		--? [*S:t-bar3_value] -le [$RetaliationDamage]|>RETALIATION_APPLY_PARTIAL_DAMAGE
		--? [*S:t-bar3_value] -gt [$RetaliationDamage]|>RETALIATION_APPLY_TEMP_DAMAGE

		--:SKIP_RETALIATION|
  	--<|

  	--:TEMP_HEALTH_RETALIATION|
  		--? [&WeaponType] -eq RANGED|>INVALID_RETALIATION
  		--? [$RetaliationDamage] -ge [*T:t-bar3_value]|>TEMP_HEALTH_RETALIATION_REMOVE
  	--<|

  	--:MELEE_RETALIATION|
  		--? [&WeaponType] -eq RANGED|>INVALID_RETALIATION
  	--<|

  	--:DISTANCE_RETALITION|
  		--? [&WeaponType] -eq RANGED|>INVALID_RETALIATION

  		--~AfterMark3|string;after;:;[&AfterMark2]
  		--~MaxDistance|string;before;:;[&AfterMark3]
  		--=MaxDistance|[&MaxDistance]

  		--? [$Distance] -gt [$MaxDistance]|>INVALID_RETALIATION
  	--<|

	--:VULN_RETLATIATION|
		--=RetaliationDamage|[$RetaliationDamage] * 2
	--<|

	--:RESIST_RETLATIATION|
		--=RetaliationDamage|[$RetaliationDamage] \ 2
	--<|

	--:IMMUNE_RETLATIATION|
		--=RetaliationDamage|0
	--<|

	--:INVALID_RETALIATION|
		--=RetaliationFlag|0
	--<|

	--:TEMP_HEALTH_RETALIATION_REMOVE|
		--+TEST|REMOVE
		--~BeforeRetaliation|string;before;TempHealthRetaliation;[&TEffectString]
		--~AfterRetaliation|string;after;:;[&AfterMark2]

		--&TEffectString|[&BeforeRetaliation][&AfterRetaliation]

		--@token-mod| _ids [*T:t-id] _set bar2_value|[&TEffectString]
	--<|

	--:RETALIATION_APPLY_DAMAGE|
		--@alter|_target|@{selected|token_id} _bar|1 _amount|-[$RetaliationDamage]
		--^MARK_SOURCE_TOKEN|
	--<|

	--:RETALIATION_APPLY_PARTIAL_DAMAGE|
		--=RemainderDamage|[$RetaliationDamage] - [*S:t-bar3_value]
		--@alter|_target|@{selected|token_id} _bar|3 _amount|-[*S:t-bar3_value]
		--@alter|_target|@{selected|token_id} _bar|1 _amount|-[$RemainderDamage]
		--^MARK_SOURCE_TOKEN|
	--<|

	--:RETALIATION_APPLY_TEMP_DAMAGE|
		--@alter|_target|@{selected|token_id} _bar|3 _amount|-[$RetaliationDamage]
		--^MARK_SOURCE_TOKEN|
	--<|

	--:MARK_SOURCE_TOKEN|
		--=HalfHP|[*S:hp^] \ 2
		--=HPTotal|[*S:t-bar1_value] + [*S:t-bar3_value]
		--=EstFinalHP|[$HPTotal] - [$RetaliationDamage]
		--? [$EstFinalHP] -le [$HalfHP] -and [$EstFinalHP] -gt 0|MARK_SOURCE_TOKEN_RED
		--? [$EstFinalHP] -le 0|MARK_SOURCE_TOKEN_DEAD
	--<|

  	--:MARK_SOURCE_TOKEN_RED|
  		--@token-mod| _ids [*S:t-id] _set statusmarkers#red
  	--<|

  	--:MARK_SOURCE_TOKEN_DEAD|
  		--@token-mod| _ids [*S:t-id] _set statusmarkers#dead|-red
  	--<|

}}