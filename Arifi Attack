!scriptcard{{
	--#title|Arifi Attack Macro
	--#sourceToken|@{selected|token_id}
	--#targetToken|@{target|token_id}

	--=TargetAC|[*T:ac]
	--? [*T:npc_ac] -lt [$TargetAC]|SkipAC
	--=TargetAC|[*T:npc_ac]
	--:SkipAC|

	--+WEAPON|[rbutton]Spear One Handed::SPEAR_ATTACK;6[/rbutton]
	--+WEAPON|[rbutton]Spear Two Handed::SPEAR_ATTACK;8[/rbutton]
	--+WEAPON|[rbutton]Scimitar::SCIMITAR_ATTACK[/rbutton]
	--+SPELLS|Cantrips
	--+CANTRIP|[rbutton]Produce Flame::PRODUCE_FLAME[/rbutton]
	--+CANTRIP|[rbutton]Magic Stone::MAGIC_STONE[/rbutton]
	--+CANTRIP|[rbutton]Chill Touch::CHILL_TOUCH[/rbutton]
	--+SPELLS|Level 1
	--+SPELL|Armor of Agathys Damage [rbutton]5::ARMOR_OF_AGATHYS;5[/rbutton] [rbutton]10::ARMOR_OF_AGATHYS;10[/rbutton] [rbutton]15::ARMOR_OF_AGATHYS;15[/rbutton]
	--+SPELL|Ice Knife [rbutton]1::ICE_KNIFE;1[/rbutton] [rbutton]2::ICE_KNIFE;2[/rbutton] [rbutton]3::ICE_KNIFE;3[/rbutton] [rbutton]4::ICE_KNIFE;4[/rbutton]
	--+SPELLS|Level 2
	--+SPELL|Flame Blade [rbutton]2::FLAME_BLADE;2[/rbutton] [rbutton]3::FLAME_BLADE;3[/rbutton] [rbutton]4::FLAME_BLADE;4[/rbutton]
	--+SPELL|Shadow Blade [rbutton]Rubilax::SHADOW_BLADE;r[/rbutton] [rbutton]2::SHADOW_BLADE;2[/rbutton] [rbutton]3::SHADOW_BLADE;3[/rbutton] [rbutton]4::SHADOW_BLADE;4[/rbutton]

	--:INIT_VALUES|
	--=Damage1Count|0
	--=Damage1Die|0
	--&Damage1Type|NULL
	--=Damage2Count|0
	--=Damage2Die|0
	--&Damage2Type|NULL
	--&SEffectString|[*S:t-bar2_value]
	--&TEffectString|[*T:t-bar2_value]
	--=BardicFlag|0
	--=BardicDie|@{Nasmyry|BardicInspirationDie}
	--=CritFlag|0
	--&AttackType|NULL
	--&WeaponType|NULL
	--&WeaponName|NULL
	--=DamageFlag|0
	--X|

	--:SPEAR_ATTACK|
		--=Damage1Count|1
		--=Damage1Die|[&reentryval]
		--&Damage1Type|Piercing
		--&DamageMod|[*S:dexterity_mod]
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb]
		--&AttackType|DEX
		--&WeaponType|MELEE
		--&WeaponName|Spear
	--^SELECT_ATTACK|

	--:SCIMITAR_ATTACK|
		--=Damage1Count|1
		--=Damage1Die|6
		--&Damage1Type|Slashing
		--&DamageMod|[*S:dexterity_mod]
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb]
		--&AttackType|DEX
		--&WeaponType|MELEE
		--&WeaponName|Scimitar
	--^SELECT_ATTACK|

	--:PRODUCE_FLAME|
		--=Damage1Count|1
		--? [*S:level] -lt 5|SKIP_EXTRA_DICE_PRODUCE_FLAME
		--=Damage1Count|2
		--? [*S:level] -lt 11|SKIP_EXTRA_DICE_PRODUCE_FLAME
		--=Damage1Count|3
		--? [*S:level] -lt 17|SKIP_EXTRA_DICE_PRODUCE_FLAME
		--=Damage1Count|4
		--:SKIP_EXTRA_DICE_PRODUCE_FLAME|
		--=Damage1Die|8
		--&Damage1Type|Fire
		--&DamageMod|0
		--&ToHitMod|[*S:spell_attack_bonus] + 1
		--&AttackType|SPELL
		--&WeaponType|RANGED
		--&WeaponName|Produce Flame
	--^SELECT_ATTACK|

	--:MAGIC_STONE|
		--=Damage1Count|1
		--=Damage1Die|6
		--&Damage1Type|Magical Bludgeoning
		--&DamageMod|[*S:wisdom_mod]
		--&ToHitMod|[*S:spell_attack_bonus] + 1
		--&AttackType|SPELL
		--&WeaponType|RANGED
		--&WeaponName|Magic Stone
	--^SELECT_ATTACK|

	--:CHILL_TOUCH|
		--=Damage1Count|1
		--? [*S:level] -lt 5|SKIP_EXTRA_DICE_CHILL_TOUCH
		--=Damage1Count|2
		--? [*S:level] -lt 11|SKIP_EXTRA_DICE_CHILL_TOUCH
		--=Damage1Count|3
		--? [*S:level] -lt 17|SKIP_EXTRA_DICE_CHILL_TOUCH
		--=Damage1Count|4
		--:SKIP_EXTRA_DICE_CHILL_TOUCH|
		--=Damage1Die|8
		--&Damage1Type|Necrotic
		--&DamageMod|0
		--&ToHitMod|[*S:spell_attack_bonus] + 1
		--&AttackType|SPELL
		--&WeaponType|RANGED
		--&WeaponName|Chill Touch
		--&Effect|The target creature cannot regain hit points until the end of the caster's next turn.
	--^SELECT_ATTACK|

	--:ARMOR_OF_AGATHYS|
		--=Damage1Count|[&reentryval]
		--=Damage1Die|1
		--&Damage1Type|Fire
		--&DamageMod|0
		--&AttackType|SPELL
		--&WeaponType|SPECIAL
		--&WeaponName|Armor of Agathys
	--^DAMAGE|

	--:ICE_KNIFE|
		--=Level|[&reentryval]
		--? [*S:lvl[$Level]_slots_expended] -lt 1|NOT_ENOUGH_SLOTS
		--=Damage1Count|1
		--=Damage1Die|10
		--&Damage1Type|Magical Piercing
		--&DamageMod|0
		--&ToHitMod|[*S:spell_attack_bonus] + 1
		--&AttackType|SPELL
		--&WeaponType|RANGED
		--&WeaponName|Ice Knife
		--@forselected+|Spawn _name|Ice Knife _qty|1 _size|3,3 _offset|0,-1 _targets|1 _bar2|[$Level]
	--^SELECT_ATTACK|

	--:FLAME_BLADE|
		--=Level|[&reentryval]
		--? [*S:lvl[$Level]_slots_expended] -lt 1|NOT_ENOUGH_SLOTS
		--=Bonus|[$Level] - 2
		--=Bonus|[$Bonus] \ 2
		--=Damage1Count|3 + [$Bonus]
		--=Damage1Die|6
		--&Damage1Type|Fire
		--&DamageMod|[*S:wisdom_mod]
		--&ToHitMod|[*S:spell_attack_bonus] + 1
		--&AttackType|SPELL
		--&WeaponType|MELEE
		--&WeaponName|Flame Blade
	--^SELECT_ATTACK|

	--:SHADOW_BLADE|
		--? [&reentryval] -eq r|RUBILAX_CAST
		--=Level|[&reentryval]
		--? [*S:lvl[$Level]_slots_expended] -lt 1|NOT_ENOUGH_SLOTS
		--@modattr| _charid [*S:id] _lvl[$Level]_slots_expended|-1 _silent
		--:RUBILAX_RETURN|
		--=Bonus|[$Level] - 2
		--=Damage1Count|2
		--? [$Bonus] -lt 1|SKIP_EXTRA_DICE_SHADOW_BLADE
		--=Damage1Count|3
		--? [$Bonus] -lt 1|SKIP_EXTRA_DICE_SHADOW_BLADE
		--=Damage1Count|4
		--? [$Bonus] -lt 1|SKIP_EXTRA_DICE_SHADOW_BLADE
		--=Damage1Count|5
		--:SKIP_EXTRA_DICE_SHADOW_BLADE|
		--=Damage1Die|8
		--&Damage1Type|Necrotic
		--&DamageMod|[*S:dexterity_mod]
		--&ToHitMod|[*S:dexterity_mod] + [*S:pb] + 1
		--&AttackType|DEX
		--&WeaponType|MELEE
		--&WeaponName|Shadow Blade
	--^SELECT_ATTACK|

	--:RUBILAX_CAST|
		--? [*S:repeating_resource_$2_resource_left] -lt 1|NOT_ENOUGH_CHARGES
		--@modattr| _charid [*S:id] _repeating_resource_$2_resource_left|-1 _silent
		--=Level|2
	--^RUBILAX_RETURN|

	--:SELECT_ATTACK|
		--#emoteText|@{selected|token_name} attacks @{target|token_name} with their [&WeaponName]
		--+ATTACK|[rbutton]Advantage::TO_HIT;2d20kh1[/rbutton]
		--+ATTACK|[rbutton]Normal::TO_HIT;1d20[/rbutton]
		--+ATTACK|[rbutton]Disadvantage::TO_HIT;2d20kl1[/rbutton]
	--X|

	--:TO_HIT|
		--&SEffectString|[*S:t-bar2_value]
		--&TEffectString|[*T:t-bar2_value]	
		--&RollString|[&reentryval] + [&ToHitMod]

		--? [&SEffectString] -inc Bless|>BLESS
		--? [&SEffectString] -inc Bane|>BANE
		--? [&SEffectString] -inc BolsteringMagic|>BOLSTERING_MAGIC

		--+ROLL|[&RollString]
		--=ToHit|[&RollString]
		--+TO HIT|[$ToHit]

		--? [$ToHit.Base] -eq 20|CRITICAL
		--? [$ToHit.Base] -eq 1|FUMBLE
		--:BARDIC_RETURN|
		--? [$ToHit] -ge [$TargetAC]|HIT
		--? [$ToHit] -lt [$TargetAC]|MISS
	--X|

	--:BLESS|
 		--=Bless|1d4
 		--+Bless|[$Bless]
 		--&RollString|+ + [$Bless.Raw]
	--<|

	--:BANE|
 		--=Bane|1d4
 		--+Bane|[$Bane]
 		--&RollString|+ - [$Bane.Raw]
	--<|

	--:BOLSTERING_MAGIC|
 		--=Bolster|1d3
 		--+Bolstering Magic|[$Bolster]
 		--&RollString|+ + [$Bolster.Raw]
	--<|

	--:CRITICAL|
		--? [&TEffectString] -inc CritNegate|NEGATE_CRIT
		--=Damage1Count|[$Damage1Count] * 2
		--=Damage2Count|[$Damage2Count] * 2
		--+CRITICAL|[rbutton]Critical Hit!::DAMAGE[/rbutton]
		--=CritFlag|1
	--X|

	--:NEGATE_CRIT|
		--+CRITICAL|Critical Hit!
		--+NEGATED|Critical Negated!
		--+HIT|[rbutton]Hit!::DAMAGE[/rbutton]
	--X|

	--:FUMBLE|
		--+FUMBLE|Critical Failure.
	--X|

	--:HIT|
		--+HIT|[rbutton]Hit!::DAMAGE[/rbutton]
	--X|

	--:MISS|
		--+MISS|Miss.
		--? [&SEffectString] -ninc BardicInspiration|SKIP_BARDIC
		--? [$BardicFlag] -eq 1|SKIP_BARDIC
		--+INSPIRED|[rbutton]Use Bardic Inspiration?::BARDIC_INSPIRATION[/rbutton]
		--:SKIP_BARDIC|
	--X| 

	--:BARDIC_INSPIRATION|
		--? [$BardicFlag] -eq 1|ERROR_BARDIC
		--=BardicFlag|1
		--=Bardic|1d[$BardicDie.Raw]
		--+Bardic Roll|[$Bardic]
		--=ToHit|[$ToHit] + [$Bardic]
		--+New To Hit|[$ToHit]

		--~Before|string;before;BardicInspiration;[&SEffectString]
		--~After|string;after;BardicInspiration;[&SEffectString]

		--&SEffectString|[&Before][&After]
		--@token-mod| _ids [*S:t-id] _set bar2_value|[&SEffectString]
	--^BARDIC_RETURN|

	--:DAMAGE|		
		--&SEffectString|[*S:t-bar2_value]
		--&TEffectString|[*T:t-bar2_value]	
		--? [$DamageFlag] -eq 1|ERROR_DAMAGE
		--=DamageFlag|1
		--=Damage1|0
		--=Damage2|0
		--=SymDamage|0

		--&Damage1String|[$Damage1Count.Raw]d[$Damage1Die.Raw]
		--? [$Damage2Count] -eq 0|SkipDamage2a
		--&Damage2String|[$Damage2Count.Raw]d[$Damage2Die.Raw]
		--:SkipDamage2a|

		--? [&SEffectString] -inc Enlarged|>ENLARGED
		--? [&SEffectString] -inc Reduced|>REDUCED
		--? [&SEffectString] -inc SymbioticEntity|>SYMBIOTIC_ENTITY

		--&Damage1String|+ + [&DamageMod]

		--=Damage1|[&Damage1String]
		--+[&Damage1Type] Damage|[$Damage1]
		--? [$Damage2Count] -eq 0|SkipDamage2b
		--=Damage2|[&Damage2String]
		--+[&Damage2Type] Damage|[$Damage2]
		--:SkipDamage2b|

		--? [&SEffectString] -inc Enfeebled|>ENFEEBLED

		--? "[*T:npc_vulnerabilities]" -inc "[&Damage1Type]"|>VULN_1
		--? "[*T:npc_vulnerabilities]" -inc "[&Damage2Type]"|>VULN_2
		--? "[*T:npc_resistances]" -inc "[&Damage1Type]"|>RESIST_1
		--? "[*T:npc_resistances]" -inc "[&Damage2Type]"|>RESIST_2
		--? "[*T:npc_immunities]" -inc "[&Damage1Type]"|>IMMUNE_1
		--? "[*T:npc_immunities]" -inc "[&Damage2Type]"|>IMMUNE_2

		--? [&TEffectString] -inc Underwater|>UNDERWATER

		--=TotalDamage|[$Damage1] + [$Damage2] + [$SymDamage]

		--? "[&TEffectString]" -inc "DT"|>APPLY_DAMAGE_THRESHOLD

		--+TotalDamage|[$TotalDamage]
	--^APPLY_DAMAGE|

	--:ENLARGED|
		--? [&AttackType] -ne STR|ENLARGED_SKIP
		--? [$CritFlag] -eq 1|ENLARGED_CRIT
		--&Damage1String|+ + 1d4
		--:ENLARGED_CRIT_RETURN|
		--:ENLARGED_SKIP|
	--<|

	--:ENLARGED_CRIT|
		--&Damage1String|+ + 2d4
	--^ENLARGED_CRIT_RETURN|

	--:REDUCED|
		--&Damage1String|+ - 1d4
	--<|

	--:SYMBIOTIC_ENTITY|
		--? [&WeaponType] -ne MELEE|SKIP_SYM
		--? [$CritFlag] -eq 1|SYM_CRIT	
		--=SymDamage|1d6
		--:SYM_CRIT_RETURN|
		--? [&Damage1Type] -inc Necrotic|SYM_DAMAGE_1
		--? [&Damage2Type] -inc Necrotic|SYM_DAMAGE_2
		--+Necrotic Damage|[$SymDamage]
		--? "[*T:npc_vulnerabilities]" -inc "Necrotic"|>VULN_SYM
		--? "[*T:npc_resistances]" -inc "Necrotic"|>RESIST_SYM
		--? "[*T:npc_immunities]" -inc "Necrotic"|>IMMUNE_SYM
		--:SKIP_SYM|
		--:SYM_RETURN|
	--<|

	--:SYM_CRIT|
		--=SymDamage|2d6
	--^SYM_CRIT_RETURN|

	--:SYM_DAMAGE_1|
		--&Damage1String|+ + [$SymDamage.Raw]
	--^SYM_RETURN|

	--:SYM_DAMAGE_2|
		--&Damage2String|+ + [$SymDamage.Raw]
	--^SYM_RETURN|
	
	--:ENFEEBLED|
		--? [&AttackType] -ne STR|SKIP_ENFEEBLED
		--+ENFEEBLED|Damage Halved!
		--=Damage1|[$Damage1] \ 2
		--:SKIP_ENFEEBLED|
	--<|

	--:VULN_1|
		--=Damage1|[$Damage1] * 2
	--<|

	--:VULN_2|
		--=Damage2|[$Damage2] * 2
	--<|

	--:VULN_SYM|
		--=SymDamage|[$SymDamage] * 2
	--<|

	--:RESIST_1|
		--=Damage1|[$Damage1] \ 2
	--<|

	--:RESIST_2|
		--=Damage2|[$Damage2] \ 2
	--<|

	--:RESIST_SYM|
		--=SymDamage|[$SymDamage] \ 2
	--<|

	--:IMMUNE_1|
		--=Damage1|0
	--<|

	--:IMMUNE_2|
		--=Damage2|0
	--<|

	--:IMMUNE_SYM|
		--=SymDamage|0
	--<|

	--:UNDERWATER|
		--? "[*T:npc_resistances]" -inc "Fire"|SKIP_UNDERWATER
		--? [&Damage1Type] -eq Fire|>RESIST_1
		--? [&Damage2Type] -eq Fire|>RESIST_2
		--:SKIP_UNDERWATER|
	--<|

	--:APPLY_DAMAGE_THRESHOLD|
		--+DT|Applying Damage Threshold...

		--~AfterString|string;after;DT;[&EffectString2]
		--~DT|string;left;2;[&AfterString]
		--=DT|[&DT]

		--? [$TotalDamage] -le [$DT]|[
			--=TotalDamage|0
			--+Resisted|@{target|character_name} Resisted the Attack!
		--]|
	--<|

	--:APPLY_DAMAGE|
		--? @{target|bar3} -gt 0|APPLY_TEMP_DAMAGE
		--@alter|_target|@{target|token_id} _bar|1 _amount|-[$TotalDamage]
		--^MARK_TOKEN|

	--:APPLY_TEMP_DAMAGE|
		--? @{target|bar3} -lt [$TotalDamage]|APPLY_TEMP_PARTIAL
		--@alter|_target|@{target|token_id} _bar|3 _amount|-[$TotalDamage]
		--^MARK_TOKEN|

	--:APPLY_TEMP_PARTIAL|
		--=RemainderDamage|[$TotalDamage] - @{target|bar3}
		--@alter|_target|@{target|token_id} _bar|3 _amount|-@{target|bar3}
		--@alter|_target|@{target|token_id} _bar|1 _amount|-[$RemainderDamage]
		--^MARK_TOKEN|

	--:MARK_TOKEN|
		--=HalfHP|[*T:hp^] \ 2
		--=HPTotal|@{target|bar1} + @{target|bar3}
		--=EstFinalHP|[$HPTotal] - [$TotalDamage]
		--? [$EstFinalHP] -le [$HalfHP] -and [$EstFinalHP] -gt 0|MARK_TOKEN_RED
		--? [$EstFinalHP] -le 0|MARK_TOKEN_DEAD
	--X|

  	--:MARK_TOKEN_RED|
  		--@token-mod| _ids [*T:t-id] _ignore-selected _set statusmarkers#red
  	--X|

  	--:MARK_TOKEN_DEAD|
  		--@token-mod| _ids [*T:t-id] _ignore-selected _set statusmarkers#dead|-red
  	--X|

  	--:ERROR_BARDIC|
  		--+ERROR|You have already used Bardic Inspiration!
  	--X|

  	--:ERROR_DAMAGE|
  		--+ERROR|You have already applied Damage!
  	--X|

  	--:NOT_ENOUGH_SLOTS|
  		--+ERROR|Not Enough Level [$Level] Slots to Cast!
  	--X|
}}